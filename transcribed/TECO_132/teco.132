
7SW==0	;PDP-7 LINK
HLSTSW==0	;HAIRY LISTING FEATURE
TS==1
IFE TS,RELOCA
MOBY==0
LPTR==1
IFN TS,[MOBY==0
LPTR==1
]

IFE TS,TITLE TECO6
IFN TS,TITLE TSTECO

GLITCH==177
ALTMOD==33
EOFCHR==3

FF=0
P"=1
A"=2
AA=3
B=4
E=5
C=6
D=7
F=10
T=11
TT=12
TT1=13
IN=14
OUT=15
CH=16
PF=17
SYM=OUT

;RIGHT HALF FLAGS

ALTF==1
ARG2==2
ARG==4
ITERF==10
SLSL==20
PCHFLG==40
COLONF==100
SYLF==200
VIEWF==400
FINF==1000
FINDR==2000
QMFLG==4000
NOTF==10000
TRACEF==20000
LET==40000
PANIC==100000	;YANK GETTING NEAR END OF BUFFER
SLASHF==200000
IFN HLSTSW,DUBSCF==400000

;LEFT HALF FLAGS

NEGF==1	;DPT-ING A NEGATIVE NUMBER
LPTF==20
UREAD==200	;READING FROM UTAPE
UWRITE==400	;WRITING ON UTAPE
PNSTOP==1000	;STOP PUNCH
GLOF==2000	;CTRL O GLOB FLG
VARF==4000	;CTRL O VAR FLG
DEFF==10000	;CTRL O DEFINE FLAG
CTLUF==100000	;DO AN ^U AT END IF VIEWF OFF
FATAL==40	;BAD INTERUPT

IFN 7SW,[7R==20000
7W==40000
0BIT==1000	;PDP-7 LOSSAGE
1BIT==2000	;PDP-7 LOSSAGE
]
TYPR1=(30000)
TYPR2=(31000)
ITYPR=(32000)
IITYPR=(33000)
IFE TS,[MACDMP==37400+MOBY
MACCR==37777+MOBY
DDT==34000+MOBY
IFE MOBY,LPDL==75
IFN MOBY,LPDL==200
]
IFN TS,LPDL==140
GCTBL==100
LPF==100

IFN HLSTSW,NSECS==200

IFN TS,[
;TS SYMBOLS

IOT=40_27.
STOPEN=41_27.
DEFINE TSOPEN A,B
	STOPEN A,B
	JSP TT,OPNER
	TERMIN
OPER=42_27.
CALL=43_27.
USET=44_27.
STATUS=46_27.

DISMISS=CALL 1,
TSVALRET=CALL 4,
CORE=CALL 6,
DSTART=CALL 10,
FDELE=CALL 11,

ITYI=OPER 1
SLEEP=OPER 3
SETMSK=OPER 4
TSCLOSE=OPER 7
DFLUSH=OPER 15
DSTOP=OPER 16
RDTIM=OPER 17
RDSW=OPER 20
UFLAP=OPER 22
RD760=OPER 30
WR760=OPER 31
RSNAME=OPER 34
WSNAME=OPER 35
RESET=OPER 37
RTIME=OPER 45
RDATE=OPER 46

SYSGE==2

PDLOV1==200000
MPV1==20000
DISMPV==20
TYPIN==1
TSMSK==PDLOV1+DISMPV+MPV1+TYPIN

TYIC==1
TYOC==2
UTYIC==3
UTYOC==4
LPTC==5
FDRC==6	;FOR READING FILE DIRECTORIES
DCHN==7
]

INIT":
LOC 41
JSR ETYPER
IFN TS,[LOC 42
	JSR TSINT
]
LOC INIT
IFE TS,	CONO 633550+APRCHN
	MOVE P,[(-LPDL)PDL
IFE TS,[CONO PTR,20
	CONO PTR,0
	CONO PI,12237
]	SETOM CTLBF'
	MOVEI A,200*5
	MOVEM A,BEG
	MOVEM A,PT
	MOVEM A,Z
	MOVEM A,QRBUF
IFN HLSTSW,[MOVEI A,SECT-1
	MOVEM A,SECTP
	MOVEI A,OSECT-1
	MOVEM A,OSECTP
IFN TS,	SETZM UROSBF
]	MOVEI A,CBUF"+77
	MOVEM A,CBUFH
	CLEARB FF,SFINDF
IFN TS,[SKIPE 52
	PUSHJ P,WINIT
]GOZ":IFE TS,[SETOM .COAST"
	CLEARM 7ST
	CONO 760,0
	CONI 760,A
	ANDI A,0BIT+1BIT
	MOVEM A,7STA
	CONO PI,2237
	SETOM .LSTP"
]	MOVE P,[(-LPDL)PDL]
	MOVE PF,[(-LPF)PFL]
IFN TS,[MOVEI B,TSMSK
	SETMSK B,
	TSOPEN TYOC,OTTY
	TSOPEN TYIC,ITTY
	STATUS TYOC,B
	ANDI B,77
	SETZM GETTY'
        CLEARM RGETTY
	CAIE B,SYSGE
	JRST GOZ2
	SETOM GETTY	;GE CONSOLE HACKS
	SETOM RGETTY'
	MOVEI B,9	;CONSERVATIVE TO AVOID AUTO CR SCREW
	MOVEM B,NLINES
	MOVEI B,5
	HRLM B,OTTY

GOZ2:	TSOPEN DCHN,OTTY
	SKIPE 51
	JRST HINIT
	MOVNI B,4000
	MOVEM B,LINES
	SKIPE RGETTY
	PUSHJ P,DIS1
	IITYPR [SIXBIT /TECO./]
$%.==<.FNAM2_<-36>>&17*100.+<.FNAM2_<-30>>&17*10.+<.FNAM2_<-22>>&17
	MOVEI B,$%.
	PUSHJ P,PRNT1
]

GOX:IFE TS,SETOM FSHFLG
	CLEARM STOPF
IFE TS,[CLEARM BRKFLG
	CONO TTY,10+TTYCHN
	SKIPG TICC
	JRST .+3
	PUSHJ P,TYI
	JRST .-3
	CLEARM FSHFLG
]	PUSHJ P,CRR

GO:	MOVE P,[-LPDL,,PDL-1]
	MOVE PF,[-LPF,,PFL-1]
IFE TS,	SKIPL FSHFLG
	SKIPGE STOPF
	JRST GOX
	TRNN FF,VIEWF
	JRST VIEW2
IFE TS,[CONO PI,4002
	CONO 433550+APRCHN
]GO1:	CLEARM LEV
	TRZ FF,777777#QMFLG#IFN HLSTSW,DUBSCF
	JRST LIS


NEWAS:	MOVEM B,NEWASM'
IFN TS,	DPB B,[260100,,ITTY]
	JRST GOZ

IFN TS,[HINIT:MOVE A,[50,,UTF1]
	BLT A,UTF2
	PUSHJ P,RRED
	PUSHJ P,YANK
	CLEARM 51
	JRST GO
]

IFE TS,[
TYI:	ILDB CH,MACCR
	JUMPN CH,CPOPJ
	SETZM MACCR
	MOVEI CH,12
	AOSN LIFF
	POPJ P,
	SKIPGE STOPF
	JRST GOX
	SKIPG TICC
	JRST .-3
	MOVE CH,TIOP
	CAMN CH,[(10700)TIBE-1]
	MOVEI CH,TIBO-1
	HRRM CH,TIOP
	ILDB CH,TIOP
	CAIN CH,15
	SETOM LIFF
	SOS TICC
	POPJ P,

6TYO:	ADDI CH,40
TYO:	CAIN CH,11
	JRST TYO2
	CAIN CH,15
	SETOM TABCNR
	CAIE CH,12
	AOS TABCNR
	SKIPG TORM
	JRST .-1
	IDPB CH,TOIP
	SOS TORM
	EXCH CH,TOIP
	CAMN CH,[(10700)TOBE-1]
	HRRI CH,TOBO-1
	EXCH CH,TOIP
STYO:	CONSO TTY,30
	CONO TTY,10+TTYCHN
	POPJ P,

TYO2:	MOVEI CH,40
	PUSHJ P,TYO
	MOVE CH,TABCNR
	TRNE CH,7
	JRST TYO2
	MOVEI CH,11
	POPJ P,
PITELE:	PUSH P,B
	CONSZ TTY,10
	JRST TYP1
	CONSO TTY,40
	JRST TTYRET
	DATAI TTY,A
	JUMPE A,PITEL3
	TRZ A,200
	SKIPLE BRKFLG
	JRST TTYRET
PITL4A:	CAIE A,175
	CAIN A,176
	MOVEI A,33
	CAIGE A,175
	CAIGE A,140
	JRST .+3
	SKIPN NEWASM
	SUBI A,40
	MOVEM B,PITEBS
	MOVE B,ECHOCC
	ADD B,TICC
	CAIL B,<TIBE-TIBO-2>*5
	TLO A,400000
	MOVE B,PITEBS
	JUMPL A,DING
	IDPB A,TIIP
	ORCMI A,177
	AOJE A,PITEL1
	CLEARM STOPF
PITEL2:	MOVE A,TIIP
	CAMN A,[(10700)TIBE-1]
	MOVEI A,TIBO-1
	HRRM A,TIIP
	AOSA ECHOCC
DING:	SETOM DINGF
	PUSHJ P,STYO
	JRST TTYRET

PITEL1:	MOVE A,TIME
	SKIPN STOPF
	MOVEM A,TSTPTM
	AOS A,STOPF
	CAIE A,3
	JRST PITEL2
	MOVE A,TIME
	SUB A,TSTPTM
	CAIL A,20.
	JRST PITEL2-1
	HRROS STOPF
	SETOM FSHFLG
	JRST PITEL2

PITEL3:	AOS BRKFLG
	MOVE A,TIME
	MOVEM A,TSTPTM
	JRST TTYRET

PITEBS:	0
TYP1:	MOVEI A,12
	AOSN LIF
	JRST TYP3
	MOVEI A,7
	AOSN DINGF
	JRST TYP3
	MOVEI A,40
	AOSG SPCCC
	JRST TYP3
	MOVE A,TORM
	CAIL A,TOBS
	JRST TYP4
	MOVE A,TOOP
	CAMN A,[(10700)TOBE-1]
	MOVEI A,TOBO-1
	HRRM A,TOOP
	ILDB A,TOOP
	AOS TORM

TYP3:	CAIN A,10
	JRST TYO1
	CAILE A,6
	CAIL A,16
	JRST TYO1
TYP31A:	CAIE A,11
	JRST TYP31
	MOVNI A,3
	MOVEM A,SPCCC
	JRST TTYRET
TYP31:	CAIN A,33
	MOVEI A,"$
	CAIN A,177
	JRST TTYRET
	MOVE B,A
	IMULI B,200401
	AND B,[11111111]
	IMUL B,[11111111]
	TLNE B,10
	TRO A,200
	SKIPL FSHFLG
	DATAO TTY,A
	JRST TTYRET

TYP4:	SKIPG ECHOCC
	JRST TYP5
	MOVE A,ECHOP
	CAMN A,[(10700)TIBE-1]
	MOVEI A,TIBO-1
	HRRM A,ECHOP
	MOVEI A,12
	HRRM A,TYP1
	ILDB A,ECHOP
	SOS ECHOCC
	AOS TICC
	CAIN A,15
	SETOM LIF
	JRST TYP3
TYP5:	CONO TTY,200+TTYCHN
TTYRET:	POP P,B
	JRST POPRET

TYO1:	CAIE A,33
	CAIL A,40
	JRST TYP31A
	ADDI A,100
	HRRM A,TYP1
	SETOM LIF
	MOVEI A,"^
	JRST TYP31

FSHFLG:	0
TOIP:	(10700)TOBO-1
TOOP:	(10700)TOBO-1
TORM:	TOBS
TIIP:	(10700)TIBO-1
TIOP:	(10700)TIBO-1
TICC:	0
ECHOP:	(10700)TIBO-1
ECHOCC:	0
TIBO:	BLOCK 26.
TIBE:
TOBO:	BLOCK 40
TOBE:
TOBS=<TOBE-TOBO>*5
LIF:	0
DINGF:	0
SPCCC:	0
TSTPTM:	0
]

STOPF:	0

IFN TS,[
TYI:	AOSN UNRCHF
	JRST UNRCH
	MOVEI CH,12
	AOSN LIFF
	POPJ P,
	SKIPGE STOPF
	JRST GOX
TYIW:	IOT TYIC,CH
	CAIN CH,15
	SETOM LIFF
TYIG:	CAIN CH,^G
	JRST TYIW
CCRR:	POPJ P,CRR

UNRCH:	HRRZ CH,UNRCHC
	JRST TYIG

UNRCHC:	0
UNRCHF:	0

6TYO:	ADDI CH,40
TYO:	SKIPGE STOPF
	POPJ P,
	SKIPL NEWASM
	JRST TYO1
	CAIL CH,100
	CAILE CH,140
	CAIN CH,"/
	JRST TYO2
TYO1:	SKIPE RGETTY
	JRST DIS3
	IOT TYOC,CH
	POPJ P,

TYO2:	HRLM CH,UNRCHC
	MOVEI CH,"/
	PUSHJ P,TYO1
	HLRZ CH,UNRCHC
	JRST TYO1

CTLL:	SKIPE GETTY
	IOT DCHN,[14]
	POPJ P,
]
PPA:IFN LPTR,[
	TLNE FF,LPTF
IFE TS,	PUSHJ P,APILPT
IFN TS,	.IOT LPTC,CH
]
PPA2:	TLNE FF,UWRITE
	PUSHJ P,UTYO
IFN TS,	POPJ P,
IFN 7SW,[TLNE FF,7W
	PUSHJ P,7WCH
	TLNE FF,PNSTOP
	POPJ P,
	IORI CH,200
	CAIN CH,214
	JRST PPA1
PPA3:	PUSH P,CH
	PUSHJ P,PIPUN
	POP P,CH
	CAIE CH,211
	POPJ P,
	MOVEI CH,377
	PUSHJ P,PPA2+2
	JRST PPA2+2

UTYO:	EXCH A,CH
	PUSHJ P,UWR"
	TYPR1 [ASCII ?TAPE FULL!?]
	EXCH A,CH
]IFN TS,[UTYO:	IDPB CH,UTYOP
	AOSGE UTYOCT
	POPJ P,
UTYOA:	MOVEM CH,UTYOP
	MOVNI CH,<UTOBE-UTOBUF>*5
	MOVEM CH,UTYOCT
	MOVE CH,[UTOBUF-UTOBE,,UTOBUF]
	IOT UTYOC,CH
	MOVE CH,[700,,UTOBUF-1]
	EXCH CH,UTYOP
]	POPJ P,

IFN HLSTSW,[IFN TS,[
UWO1:	PUSHJ P,UTYOA
UWO:	AOS AA,UTYOP
	HRRZS AA
	CAIN AA,UTOBE
	JRST UWO1
	MOVEM CH,(AA)
	POPJ P,
]]

IFE TS,[PPA1:	PUSH P,B
	MOVEI B,12
	PUSHJ P,FEED1
	MOVEI CH,214
	PUSHJ P,PIPUN
	MOVEI CH,377
	MOVEI B,4
	PUSHJ P,FEED1+1
	MOVEI B,30
	PUSHJ P,FEED1
	POP P,B
	POPJ P,
]
CRR:	MOVEI CH,TYO
	HRRM CH,LISTF5
CRR1:	MOVEI CH,15
	PUSHJ P,@LISTF5
	MOVEI CH,12
	JRST @LISTF5

SKRCH:	SKIPG COMCNT
	TYPR2 1,(SIXBIT \UEC\)	;UNEXPECTED END OF COMMAND
RCH:	SOSGE COMCNT
	JRST RCH2
	ILDB CH,CPTR
TRACS:	POPJ P,TYO	;OR JRST TYO IN TRACE MODE

RCH2:	POP P,CH
	POP P,MARG2
	POP P,MARG1
	POP P,COMCNT
	POP P,CPTR
	POP P,COMAX
	PUSH P,CH
	JRST RCH

SKRCH1:	SOSGE COMCNT
	TYPR2 2,(SIXBIT \UEC\)	;ALSO
	ILDB CH,CPTR
	POPJ P,

CLIS:	PUSHJ P,CRR
LIS:	CLEARM COMCNT
	CLEARM INTDPH
	CLEARM SYMS
	MOVE T,[SYMS,,SYMS+1]
	BLT T,SYMEND-1
	MOVE AA,[10740,,CBUF-1]	;3.6 BIT TO FOOL GC
	MOVE B,CBUFH
LI1:	TRZ FF,ALTF+SLASHF
LI2:	CAILE B,(AA)
	JRST LI3
IFE TS,	CLEARM BARPNT
	ADDI B,100
	MOVE C,Z
	IDIVI C,5
	MOVE D,QRBUF
	IDIVI D,5
	SUBM C,D
	ADDI C,CBUF
	MOVE CH,(C)
	MOVEM CH,100(C)
	SOS C
	SOJGE D,.-3
	MOVEI C,500
	ADDM C,BEG
	ADDM C,PT
	ADDM C,Z
	ADDM C,QRBUF

LI3:	MOVEM B,CBUFH
	PUSHJ P,TYI
	TRZE FF,QMFLG
	CAIE CH,77
	AOSA COMCNT
	JRST ERRTYP
	SKIPGE NEWASM
	JRST LI3A
LI3B:	CAIE CH,177
	JRST LI4
	CAMN AA,[10740,,CBUF-1]
	JRST CLIS
	LDB CH,AA
	PUSHJ P,TYO
	ADD AA,[70000,,]
	SKIPGE AA
	SUB AA,[430000,,1]
	SOS COMCNT
	SOS COMCNT
	TRZ FF,ALTF
	JRST LI2
LI4:	IDPB CH,AA
	CAIE CH,ALTMOD
	JRST LI1
	TRON FF,ALTF
	JRST LI2
	IDPB CH,AA	;EXTRA ALTMOD FOR GOOD MEASURE
	AOS A,COMCNT
	MOVEM A,COMAX
	MOVE AA,[10740,,CBUF-1]
	MOVEM AA,CPTR
IFN TS,	SKIPN RGETTY
	PUSHJ P,CRR
IFE TS,	CONO DIS,0
IFN TS,[SKIPN GETTY
	DSTOP
]	TRZ FF,VIEWF
	JRST CD

LI3A:	CAIN CH,177
	JRST LI3R
	TRZE FF,SLASHF
	JRST LI3B
	CAIN CH,"/
	JRST LI3S
	CAIGE CH,137
	CAIGE CH,100
	JRST LI3B
	ADDI CH,40
	JRST LI3B

LI3S:	TRO FF,SLASHF
	JRST LI2

LI3R:	TRZE FF,SLASHF
	JRST LI3R1
	LDB CH,AA
	CAIGE CH,137
	CAIGE CH,100
	CAIN CH,"/
	TRO FF,SLASHF
	MOVEI CH,177
	JRST LI3B

LI3R1:	MOVEI CH,"/
	PUSHJ P,TYO
	JRST LI2

ERRTYP:	MOVE AA,ERR2
	MOVEI B,12
	SUBI AA,2
	ILDB CH,AA
	CAMG B,ERR1
	PUSHJ P,TYO
	CAME AA,ERR2
	SOJA B,.-4
	JRST ERR+1

1INS:	SKIPL DTB(CH)	;NEGATIVE INSTRUCTIONS DO NOT RETURN VALUE
	JRST VALRET	;POSITIVE ONES RETURN VALUE
CD:
RET:	SKIPGE STOPF
	JRST GOX
	TRZ FF,ARG2+ARG+FINDR
CD1:	CLEARM NUM
CD2:	MOVSI A,(ADD B,)
CD3:	HLLM A,DLIM
CD4:	CLEARM SYL
	CLEARM OSYL
CD5:	MOVEI AA,0
	PUSHJ P,RCH
	TRZ FF,FINF+PANIC+FINDR+PCHFLG
	XCT DTB(CH)
	TLZ FF,CTLUF
CD6:	MOVE B,NUM
	TRZE FF,SYLF
DLIM:	ADD B,SYL
	MOVEM B,NUM
	MOVE C,SARG
	TRZ FF,VIEWF+NOTF+PCHFLG+FINDR
	JUMPGE AA,(AA)
	PUSHJ P,(AA)
	JRST RET

CDNUM:	MOVE A,OSYL
	LSH A,3
	ADDI A,-60(CH)
	MOVEM A,OSYL
	MOVE A,SYL
	IMULI A,12
	ADDI A,-60(CH)
VALRET:	MOVEM A,SYL
CD7:	TRO FF,ARG+SYLF
	JRST CD5

COMMA:	MOVEM B,SARG
	TRZE FF,ARG
	TROE FF,ARG2
	TYPR2 1,(SIXBIT \WNA\)
	JRST CD1

CAND:	MOVSI A,(AND B,)
	JRST CD3
COR:	MOVSI A,(IOR B,)
	JRST CD3
MINUS:	MOVSI A,(SUB B,)
	JRST CD3
TIMES:	MOVSI A,(IMUL B,)
	JRST CD3
SLASH:	MOVSI A,(IDIV B,)
	JRST CD3
CXOR:	MOVSI A,(XOR B,)
	JRST CD3

HOLE:	CLEARM SARG
	TRO FF,ARG2
END1:	MOVE A,Z
	SUB A,BEG
	JRST VALRET

OPEN:	PUSH P,NUM
	HLLZ A,DLIM
	TRZE FF,ITERF
	IORI A,1
	PUSH P,A
	AOS LEV
	JRST RET

CLOSE:	SOSGE LEV
	TYPR2 1,(SIXBIT \UMC\)	;UNMATCHED CLOSE PAREN
	MOVEM B,SYL
	POP P,CH
	HLLM CH,DLIM
	TRZ FF,ITERF
	TRNE CH,1
	TRO FF,ITERF
	POP P,NUM
	JRST CD7

CHK:	CAMG B,Z
	CAMGE B,BEG
	TYPR2 1,(SIXBIT \NIB\)	;NOT IN BUFFER
CRDPT:	POPJ P,RDPT

CHK1:	CAMG C,BEG
	MOVE C,BEG
	CAML B,Z
	MOVE B,Z
	CAMLE C,B
	TYPR2 (SIXBIT \2<1\)	;2ND ARG LESS THAN 1ST
IFE TS,[CCRR:]	POPJ P,CRR

PNT:	MOVE A,OSYL
	TRNE FF,SYLF
	JRST VALRET
	MOVE A,PT
	JRST END1+1
APPEND:	TRNE FF,ARG
	JRST APPND1
	MOVE OUT,Z
	PUSHJ P,YANK2
	JRST RET
APPND1:	ADD B,PT
	SOS IN,B
	PUSHJ P,CHK
	PUSHJ P,GET
	MOVE A,CH
APPND2:	CLEARM NUM
	JRST VALRET

YANK:YANK1:IFN TS,CLEARM GEA
	MOVE OUT,BEG
	MOVEM OUT,PT
YANK2:IFN TS,[TLNE FF,UREAD
	JRST YNKF
]	TRZ FF,PANIC
	MOVE F,MEMSIZ
	SUB F,OUT
	SUBI F,200.*5
	TRNN FF,ARG
	MOVE B,F
IFN 7SW,[TLNE FF,7R
	MOVEI F,7RCH
]	TLNN FF,UREAD
IFE TS,	MOVEI F,PIRPA
IFN TS,	TYPR2 (SIXBIT \NDO\)
IFE TS,	TLNE FF,UREAD
	MOVEI F,UTYI
YANK3:	PUSHJ P,(F)
	PUSHJ P,PUT
	ADDI OUT,1
	TRNN FF,PANIC
	JRST .+4
	CAIE CH,13
	CAIN CH,12
	JRST YANK6	;FOUND LF OR VT
	CAIE CH,14
	SOJG B,YANK3
	JUMPG B,YANK5
	TROE FF,PANIC
	JRST YANK6
	MOVEI B,160.*5
	JRST YANK3
YANK5:	SUBI OUT,1
YANK4:	MOVEM OUT,Z
IFE TS,	CAMN OUT,BEG
	TLNE FF,UREAD
	POPJ P,
IFE TS,[MOVN OUT,RCC
	CAIN OUT,10
	TRO FF,FINF
	POPJ P,
]YANK6:	ITYPR (SIXBIT \URK\)	;BUFFER GETTING BLOATED
YANK7:	JRST YANK4
DEFINE DBP7 A
	ADD A,[70000,,]
	SKIPGE A
	SUB A,[430000,,1]
TERMIN

IFN TS,[
YNKF:	SETZM UTIEF
	PUSH P,E
	MOVE F,OUT
	PUSHJ P,GETBP
	MOVE D,[YPG,,A]
	BLT D,C
	MOVE D,UTYIP
	JRST A

YPG:	ILDB CH,D	;A
	CAIN CH,14	;AA
	JRST YPG1	;B
	IDPB CH,F	;E
	JRST A	;C

YPG1:	MOVEM D,UTYIP
	HRRZ CH,D
	CAIN CH,UTIBE
	JRST YPG2	;JUST EOB
	DBP7 D
	LDB CH,D
	CAMN CH,SYSEOF'
	JRST YPG3	;EOF
YPG1A:	PUSHJ P,GETCA
	AOS OUT,F
	POP P,E
	JRST YANK4

YPG2:	PUSHJ P,UTRLD	
	SKIPL UTIEF
	JRST YPG2A
	MOVE D,UTYIP
	ILDB TT1,D
	CAME TT1,SYSEOF
	JRST .-2
	MOVEI TT1,14
	IDPB TT1,D
YPG2A:	TRNE FF,FINF
	JRST YPG3
	DBP7 F
	MOVE D,UTYIP
	JRST A+1

YPG3:IFN HLSTSW,MOVEM D,UTYIP
	PUSHJ P,UTYIA
	DBP7 F
	JRST YPG1A
]
GETBP:	MOVE TT,F
	IDIVI TT,5
	MOVE F,BTAB(TT1)
	HRRI F,CBUF(TT)
	DBP7 F
	TLZ F,17
	POPJ P,

REVERS:	PUSHJ P,CHK2
	MOVNS B
CHARAC:	PUSHJ P,CHK2
	ADD B,PT
JMP1:	PUSHJ P,CHK
	MOVEM B,PT
	JRST RET
JMP:	ADD B,BEG
	JRST JMP1
CHK2:	TROE FF,ARG
	POPJ P,
	LDB B,[(340200)DLIM]
	MOVNS B
	AOJA B,CPOPJ

KILL:IFN TS,CLEARM GEA
	PUSHJ P,GETARG
	PUSHJ P,CHK1
	MOVEM C,PT
	SUB B,C
	JUMPE B,RET
DELETE:	PUSHJ P,CHK2
	MOVM C,B
	MOVNS C
	ADD B,PT
	PUSHJ P,CHK
	CAMGE B,PT
	MOVEM B,PT
	PUSHJ P,NROOM
DEL2:
JRET:	JRST RET


IFE TS,[FEED:
FEED1:	MOVEI CH,0
	PUSHJ P,PPA3
	SOJG B,.-1
	POPJ P,

UTYI:	PUSHJ P,URED"
	TRO FF,FINF
	MOVE CH,A
	POPJ P,
]

IFN TS,[
UTYI:	ILDB CH,UTYIP
	CAIE CH,SYSEOF
	POPJ P,
UTYIA:	HRRZ CH,UTYIP
	CAIN CH,UTIBE
	JRST UTRLD
IFN HLSTSW,PUSHJ P,UROSB
UICLS:	TRO FF,FINF
	TSCLOSE UTYIC,
	SETOM ERDEV
IFN HLSTSW,MOVE CH,[250700,,[EOFCHR_14.+14_7]]
IFE HLSTSW,MOVE CH,[160700,,[EOFCHR_7+14]]
	MOVEM CH,UTYIP
	MOVEI CH,EOFCHR
	MOVEM CH,SYSEOF
	MOVEI CH,14
	POPJ P,

UTRLD:	MOVE CH,[UTIBUF-UTIBE,,UTIBUF]
	IOT UTYIC,CH
	JUMPGE CH,UTRLD1
	SETOM UTIEF
	HRLI CH,14_4.
	PUSH P,A
	MOVE A,SYSEOF
	DPB A,[350700,,CH]
	POP P,A
	HLLM CH,(CH)
UTRLD1:	MOVE CH,[700,,UTIBUF-1]
	MOVEM CH,UTYIP
	JRST UTYI
UTIEF:	0
]
IFN HLSTSW,[IFN TS,[
UROSB:	SKIPN UROSBF
	POPJ P,
	SETZM UROSBF
	PUSHJ P,UTYIR
	CAIE CH,14
	POPJ P,
	PUSHJ P,UTYIR
	CAME CH,SYSEOF
	POPJ P,
	PUSHJ P,UTYIR
	CAIE CH,14
	POPJ P,
	PUSHJ P,UTYIR
	CAIE CH,14
	POPJ P,
UROSB1:	PUSH P,A
	PUSH P,B
	PUSHJ P,UTYIW
	MOVE A,CH
UROSB0:	JUMPLE A,UROSB2
	HRRZ B,OSECTP
	CAIL B,OSECPE
	TYPR2 (SIXBIT \TMO\)
	PUSHJ P,UTYIW
	PUSH B,CH
	HRRZM B,OSECTP
	SOJA A,UROSB0

UROSB2:	POP P,B
	POP P,A
	POPJ P,

UROSBF:	0
UTYIRR:	PUSHJ P,UTYIRW
UTYIR:	ILDB CH,UTYIP
	CAME CH,SYSEOF
	POPJ P,
	HRRZ CH,UTYIP
	CAIN CH,UTIBE
	JRST UTYIRR
	MOVE CH,SYSEOF
	POPJ P,

UTYIWW:	PUSHJ P,UTYIRW
UTYIW:	AOS CH,UTYIP
	HRRZS CH
	CAIN CH,UTIBE
	JRST UTYIWW
	MOVE CH,(CH)
	POPJ P,

UTYIRW:	MOVE CH,[UTIBUF-UTIBE,,UTIBUF]
	IOT UTYIC,CH
	MOVE CH,[700,,UTIBUF-1]
	MOVEM CH,UTYIP
	POPJ P,
]]

TAB:	PUSHJ P,TAB2
INSERT:	TRNE FF,ARG
	JRST INS1A
	MOVEI CH,ALTMOD
	TRZE FF,SLSL
	PUSHJ P,RCH
	MOVEM CH,A
	MOVE B,CPTR
	MOVEI C,0
	PUSHJ P,SKRCH
	CAME CH,A
	AOJA C,.-2
	PUSHJ P,NROOM
	ADD B,CRREL
INS1B:	MOVE OUT,PT
INS1B1:	ILDB CH,B
	CAMN CH,A
	POPJ P,
	PUSHJ P,PUT
	AOS OUT,PT
	JRST INS1B1


INS1A:
TAB1:	MOVE CH,NUM
TAB2:	MOVEI C,1
	PUSHJ P,NROOM
	AOS OUT,PT
	SOJA OUT,PUT

TYOM:	PUSH P,C
	PUSH P,OUT
	PUSH P,TT
	PUSH P,TT1
	PUSHJ P,TAB2
	POP P,TT1
	POP P,TT
	POP P,OUT
	POP P,C
	POPJ P,

LARR:	TROA FF,FINDR
SERCHP:	TRO FF,PCHFLG
SERCH:	TRZ FF,FINF+NOTF
	PUSHJ P,CHK2
	MOVEI CH,ALTMOD
	TRZE FF,SLSL
	PUSHJ P,RCH
	HRRM CH,SBRK
	MOVEI C,GCTAB
	HRRM C,SRGO2A
	JUMPL B,RSRCH3
	MOVE F,Z
	ADDI F,1
SRCH7:	PUSHJ P,GETBP
	MOVEM F,SBP'

SRCH1:	HRRM C,SRLP2A	;SEARCH TABLE FORMAT
	MOVEM C,(C)	;.+4,,.
SRCH2:	PUSHJ P,RCH	;CAIN CH,"F
SBRK:	CAIN CH,.	;CAIE CH,"O
	JRST SRLP1	;CAIN CH,"O
	CAIN CH,^O	;.+6,,.
	JRST SRLP2	;CAIN CH," 	;SPACE
	CAIN CH,^X	;CAIE CH,"S
	JRST SRLP3	;CAIN CH,"P
	CAIN CH,^N	;PUSHJ P,CNTRB1
	JRST SRLP4	;CAIA CH,^X
	CAIN CH,^B	;CAIN CH,")
	JRST SRLP5	;0
	CAIN CH,^Q	;MEANS SFOO SP)$$
	PUSHJ P,RCH
	HRLI CH,(CAIN CH,)
SRCH3:	TRZE FF,NOTF
	TLC CH,(CAIE#CAIN)
	CAIL C,GCTAB+GCTBL
	TYPR2 (SIXBIT \STL\)	;SEARCH STRING TOO LONG
	MOVEM CH,1(C)
	AOJA C,SRCH2

SRLP5:	SKIPA CH,[JSR P,CNTRB1]	;CAIE#CAIN=PUSHJ#JSR
SRLP3:	HRLI CH,(CAI CH,)	;CAIE#CAIN=CAIA#CAI
	JRST SRCH3
SRLP4:	TRO FF,NOTF
	JRST SRCH2

SRLP1:	TLO C,400000
SRLP2:	ADDI C,1
SRLP2A:	HRLM C,.
	JUMPGE C,SRCH1
	JUMPE B,BEGIN1
	CLEARM (C)
	SKIPA F,PT
SRLP6:	MOVE F,BEG
	JUMPL B,.+2
	ADDI F,1
	PUSHJ P,GETBP

SRGO1:	CAMN F,SBP
	JRST LOSE
	CLEARM SNUM'
SRGO2A:	SKIPA C,.
SRGO2:	MOVE C,.
	JUMPE C,SRGO4
	LDB CH,F
	HRRM C,WIN2
	SOS SNUM
	HLRZ A,C
	HRRM A,SRGO2
	MOVE E,F
SRGO3:	CAIG A,1(C)
	JRST WIN
	JUMPL B,RSRCH1
	XCT 1(C)
	CAMN E,SBP
	JRST SRGO2
SRGO3A:	ILDB CH,E
	AOJA C,SRGO3

SRGO4:	JUMPL B,SRGO4A
	IBP F
	JRST SRGO1
SRGO4A:	DBP7 F
	JRST SRGO1

LOSE:	CLEARM SFINDF
	JUMPL B,BEGIN1
	TRNE FF,PCHFLG+FINDR
	JRST LOSE1
BEGIN1:	TRZN FF,COLONF
	JRST LOSE2
BEGIN2:
BEGIN:	MOVEI A,0
	JRST VALRET

LOSE1:	SKIPGE STOPF
	JRST GOX
	TRNE FF,FINF
	JRST BEGIN1
	PUSH P,B
	MOVEI B,1
	TRZ FF,ARG
	TRNE FF,PCHFLG
	PUSHJ P,PUNCHA
	TRNE FF,FINDR
	PUSHJ P,YANK1
	POP P,B
	MOVE F,Z
	ADDI F,1
	PUSHJ P,GETBP
	MOVEM F,SBP
	JRST SRLP6

LOSE2:	TRNE FF,ITERF
	JRST BEGIN2
	TYPR2 (SIXBIT \SFL\)	;SEARCH FAILED

RSRCH1:	XCT -1(A)
	CAMN E,SBP
	JRST SRGO2
RSRCH2:	DBP7 E
	LDB CH,E
	SOJA A,SRGO3

RSRCH3:	MOVE F,BEG
	JRST SRCH7

RWIN:	AOJL B,SRGO1
	IBP F
	MOVN A,SNUM
	JRST WIN1

WIN:	MOVE F,E
	CLEARM @SRGO2
WIN2:	MOVEI C,.
	HRRM C,SRGO2A
	SETOM SFINDF
	JUMPL B,RWIN
	SOJG B,SRGO1
	MOVE A,SNUM
WIN1:	PUSHJ P,GETCA
	MOVEM F,PT
	TRZE FF,COLONF
	JRST VALRET
	JRST RET

GETCA:	LDB TT,[360600,,F]
	TLZ F,-1
	IMULI F,5
	IDIVI TT,7
	SUBI F,CBUF*5-4(TT)
	POPJ P,

CNTRB1:	JRST CNTRB2
	MOVE TT,[JRST CNTRB2]
	MOVEM TT,.-2
	PUSHJ P,DQT2
	JRST SRGO2
	JRST CNTRB3

CNTRB2:	SUB P,[1,,1]
	PUSHJ P,DQT2
CNTRB3:	CAMN E,SBP
	JRST SRGO2
	JUMPL B,RSRCH2
	JRST SRGO3A
TYPE:
TYPE4:	MOVEI D,TYO
	PUSHJ P,GETARG
TYPE1:IFN TS,CLEARM GEA
TYPE1A:	PUSHJ P,CHK1
	MOVE IN,C
IFN TS,[	MOVEI A,PPA
	CAIN A,(D)
	TLNE FF,LPTF
	JRST TYPE3
IFN HLSTSW,[MOVE TT,SECTP
	CAIE TT,SECT-1
	JRST TYPE3
]	TLNE FF,UWRITE
	JRST PUNCHF
]
TYPE3:	CAML IN,B
	JRST TYPE5
	PUSHJ P,GETINC
IFN HLSTSW,[IFN TS,[
	CAIE D,CPPA
	CAIN D,PPA
	JRST SECCH
TYPE3R:]]PUSHJ P,(D)
IFE TS,[SKIPGE BRKFLG
	JRST TYPE6]
	JRST TYPE3

TYPE5:	MOVEI A,PPA
	MOVEI CH,14
	CAIE A,(D)
	POPJ P,
CPPA:	JRST PPA

TYPE6:	MOVEM IN,PT
	JRST GOX

USE:	TRNN FF,ARG
	TYPR2 2,(SIXBIT \WNA\)
USEA:	PUSHJ P,QREGA
	MOVEM B,QTAB-60(CH)
	JRST RET

QREG:	PUSH P,USE1
QREGA:	PUSHJ P,LRCH
	CAIL CH,"0
	CAILE CH,"Z
	TYPR2 (SIXBIT \IQN\)
	CAILE CH,"9
	SUBI CH,"A-"9-1
	MOVE A,QTAB-"0(CH)
USE1:	POPJ P,VALRET

NQREG=="Z-"A+1 "9-"0+1

IFN HLSTSW,[IFN TS,[
SECCH:	EXCH CH,CHKSUM
	ROT CH,1
	ADD CH,CHKSUM
	EXCH CH,CHKSUM
	SKIPE SECCHP
	JRST SECCH1
	SKIPG SEMMOD
	CAIE CH,";
SECCH2:	TRZA FF,DUBSCF
	TRCN FF,DUBSCF
	JRST TYPE3R
SECCH4:	MOVEM A,SECCHA
	MOVE A,SECTP
	CAIL A,SECTPE
	TYPR2 (SIXBIT \TMS\)
	PUSH A,SECNAM
	PUSH A,CHKSUM
	HRRZM A,SECTP
	MOVE A,[440700,,SECNAM]
	MOVEM A,SECCHP
	MOVEI A,5
	MOVEM A,SECCHC
	SETZM CHKSUM
	SETZM SECNAM
	MOVE A,SECCHA
	JRST TYPE3R

SECCH1:	CAIG CH,40
	JRST SECCH3
	IDPB CH,SECCHP
	SOSG SECCHC
SECCH3:	SETZM SECCHP
	JRST SECCH2

SECTE:	0
SECTP:	SECT-1
SECNAM:	";_35
CHKSUM:	0
OSECTP:	OSECT-1
SECCHP:	0
SECCHA:	0
SECCHC:	0
]]

IFN TS,[PUNCHF:	CAML IN,B
	JRST TYPE3
IFN HLSTSW,[SETZM PUNFPC
	MOVEM B,PUNFPB
]	MOVE F,IN
	SUBM B,IN	;IN GETS CHAR COUNT
	PUSHJ P,GETBP
	PUSH P,E
	MOVE C,[PPG,,A]
	BLT C,C
PCHF1:	MOVE TT,UTYOP
	MOVN OUT,UTYOCT	;GETS POS COUNT
	CAMLE OUT,IN
	MOVE OUT,IN
	PUSH P,OUT
	JUMPE OUT,PPG1
	JRST A

PPG:	ILDB CH,F	;A
	IDPB CH,TT	;AA
IFN HLSTSW,CAIE CH,";	;B
	SOJG OUT,A	;E OR B
	JRST PPG1	;C OR E

PPG1:IFN HLSTSW,JUMPG OUT,PPG2
	POP P,OUT
	MOVEM TT,UTYOP
	ADDM OUT,UTYOCT
	SKIPL UTYOCT
	PUSHJ P,UTYOA
	SUB IN,OUT
	JUMPG IN,PCHF1
	POP P,E
	JRST TYPE5

IFN HLSTSW,[PPG2:SKIPL SEMMOD
	JRST E
	IBP PUNFPC
	CAMN F,PUNFPC
	JRST PPG3
	MOVEM F,PUNFPC
	JRST E

PPG3:	DBP7 TT
	MOVEM TT,UTYOP
	MOVNI OUT,(OUT)
	ADDM OUT,UTYOCT
	POP P,OUT
	ADDM OUT,UTYOCT
	PUSHJ P,GETCA
	MOVEI IN,1(F)
	POP P,E
	MOVE B,PUNFPB
	MOVEI D,PPA
	JRST SECCH4

PUNFPC:	0
PUNFPB:	0
]]

PUNCH:
PUNCHA:	IFN TS,[
	TLNN FF,LPTF\UWRITE
	TYPR2 (SIXBIT /NDO/)
]IFN HLSTSW,TRZ FF,DUBSCF
	MOVEI D,CPPA
	TRNE FF,ARG2
	JRST TYPE1-1
	MOVE E,B
	MOVE B,CPTR
	ILDB T,B
	JUMPL E,CPOPJ
PUN1:	PUSHJ P,PUNCHR
	TRZ FF,ARG
	CAIE T,"W
	CAIN T,"W+40
	SKIPN COMCNT
	PUSHJ P,YANK1
	MOVE C,Z
	CAMN C,BEG
	TRNN FF,FINF
	SOJG E,PUN1
CPOPJ:	POPJ P,VIEW1
PUNCHR:	SKIPGE STOPF
	JRST GOX
	MOVE C,BEG
	MOVE B,Z
	MOVEI D,PPA
	JRST TYPE1

DPT2:	MOVNS B
	TLO FF,NEGF
	SKIPGE B
	MOVEI B,0
RDPT:	SOJA TT,DPT1
DPT:	TDZA TT,TT
SLDPT:	MOVEI TT,2
DPT1:	JUMPL B,DPT2
	IDIVI B,10.
	HRLM E,(P)
	SKIPE B
	PUSHJ P,RDPT
	TLZE FF,NEGF
	PUSH P,["--"0,,DPT3]
DPT3:	JUMPLE TT,DPT4
DPT5:	MOVEI CH,40
	PUSHJ P,@LISTF5
	SOJG TT,.-1
DPT4:	HLRE CH,(P)
	ADDI CH,"0
	JRST @LISTF5

PRNT:IFN HLSTSW,[TRZE FF,COLONF
	JRST PRNT1
]	TRNN FF,ARG
	TYPR2 3,(SIXBIT \WNA\)
PRNT1:	MOVEI A,TYO
	HRRM A,LISTF5
	PUSHJ P,DPT
	JRST CRR

IFN HLSTSW,[PRNT1:	IFE TS,MOVEI A,0
	IFN TS,MOVNS A,SEMMOD
	SUB P,[1,,1]
	JRST VALRET
IFN TS,SEMMOD:	-1
]
DD:	IFN TS,[
	TRNN FF,ARG
	JRST DD1
]	ANDI B,3
	ROT B,4
	MOVEM B,CHSIZ
	JRST RET

IFN TS,[
DD1:	SETCMM DSW'
	JRST RET
]

DQUOTE:	TRNN FF,ARG
	TYPR2 4,(SIXBIT \WNA\)
	PUSHJ P,LRCH
	MOVSI A,0
	IRPC Z,,[GLNE]
	CAIN CH,"Z
	MOVSI A,(JUMP!Z B,)
	TERMIN
	CAIN CH,"C
	JRST DQT1
	JUMPE A,ERR+1
	HRRI A,RET
	XCT A
NOGO:	MOVEI A,0
	PUSHJ P,SKRCH1
	CAIN CH,42
	AOJA A,.-2
	CAIN CH,47
	SOJL A,RET
	JRST .-5
EXCLAM:	PUSHJ P,SKRCH
	CAIE CH,"!
	JRST .-2
	JRST RET

LRCH:	PUSHJ P,SKRCH
	CAIL CH,140
	SUBI CH,40
	POPJ P,
DQT1:	PUSHJ P,DQT3
	JRST RET
	JRST NOGO

DQT3:	MOVE CH,B
DQT2:	CAIE CH,"$
	CAIN CH,"%
	POPJ P,
	CAIN CH,".
	POPJ P,
	CAIGE CH,"0
	JRST POPJ1
	CAIG CH,"9
	POPJ P,
	CAIGE CH,"A
	JRST POPJ1
	CAIG CH,"Z
	POPJ P,
	CAIGE CH,"A+40
	JRST POPJ1
	CAIG CH,"Z+40
	POPJ P,
	JRST POPJ1

OG:	MOVE A,CPTR
	MOVE AA,A
	IDIVI AA,17
	CAMN A,SYMS(B)
	JRST OGFND
	SKIPN SYMS(B)
	JRST OGNF
	CAMN A,SYMS+1(B)
ES1:	AOJA B,OGFND
	SKIPN SYMS+1(B)
ES2:	AOJA B,OGNF
	CAMN A,SYMS+2(B)
	AOJA B,ES1
	SKIPN SYMS+2(B)
	ADDI B,2

OGNF:	PUSH P,CPTR
	PUSH P,B
	MOVEI D,STAB+1
	MOVEI A,41
	MOVEM A,-1(D)
	PUSHJ P,SKRCH
	MOVEM CH,(D)
	CAIE CH,ALTMOD
	AOJA D,.-3
	MOVEM A,(D)
	MOVE B,COMCNT
	SUB B,COMAX
	IDIVI B,5
	ADD B,CPTR
	JUMPE E,OG2
	SOS B
	MOVMS E
	JRST .(E)
	IBP B
	IBP B
	IBP B
	IBP B
OG2:	MOVE AA,COMAX
OG4:	MOVEM B,CPTR
	MOVEM AA,COMCNT
	MOVEI E,STAB
OG5:	CAIN E,1(D)
	JRST OG3
	PUSHJ P,SKRCH1
	CAMN CH,(E)
	AOJA E,OG5
	IBP B
	SOJA AA,OG4

OG3:	POP P,A
	POP P,SYMS(A)
	MOVEM AA,CNTS(A)
	MOVEM B,VALS(A)
	JRST RET

PCNT:	PUSHJ P,QREGA
	AOSA A,QTAB-60(CH)
LAT:	DATAI A
	JRST VALRET

IFN TS,[
RTIMER:	.RDTIME A,
	LSH A,1
	JRST VALRET
]

OGFND:	MOVE A,VALS(B)
	MOVEM A,CPTR
	MOVE A,CNTS(B)
	MOVEM A,COMCNT
	JRST RET
CHSIZ:	20ооVIEW:	TRO FF,VIEWF
VIEWP1:	PUSHJ P,DISINI
	MOVEI D,DISAD
	TRNE FF,VIEWF
	PUSHJ P,GETARG
	PUSHJ P,TYPE1A

VIEW1:	PUSHJ P,DISCLG
	TRNN FF,VIEWF
	JRST GO1
	MOVE T,CPTR
	ILDB B,T
	CAIE B,"W
	CAIN B,"W+40
	SKIPN COMCNT
	JRST RET
	PUSHJ P,TYI
	MOVE A,CH
IFE TS,	CONO DIS,0
IFN TS,[SKIPN GETTY
	DSTOP
]	PUSHJ P,RCH
	JRST APPND2

VIEW2:IFE TS,CONO DIS,0
	TLZE FF,CTLUF
	JRST CNTRLV
IFN TS,[SKIPN GETTY
	JRST VIEW2A
	HRRZ B,GEA'
	ADD B,BEG
	HLRZ C,GEA
	ADD C,BEG
	MOVE IN,PT
	CAILE IN,2*MGEC(C)
	CAIG B,2*MGEC(IN)
VIEW2A:	SKIPA B,NLINES
	JRST VIEWP1
]IFE TS,MOVE B,NLINES
	PUSHJ P,GETAG7
	PUSH P,B
	MOVN B,NLINES
	PUSHJ P,GETAG7
	POP P,B
IFN TS,	SKIPN GETTY
	JRST VIEWP1
IFN TS,[MOVE AA,NLINES
	IMULI A,MGEC
	HRRM AA,VIEW2B
	MOVE IN,PT
VIEW2B:	CAILE B,.(IN)
	MOVEI B,@VIEW2B
	SUB IN,AA
	CAMGE C,IN
	MOVE C,IN
	MOVE AA,C
	SUB AA,BEG
	HRLZM AA,GEA
	MOVE AA,B
	SUB AA,BEG
	HRRM AA,GEA
	JRST VIEWP1
]
NLINES:	20.

ERR:	0
IFE TS,	PUSHJ P,UWAIT"
ERRP1:	MOVEI CH,277
	PUSHJ P,TYO
	PUSHJ P,CRR
IFN TS,	PUSHJ P,CRR
	TRO FF,QMFLG
	MOVE A,COMAX
	SUB A,COMCNT
	MOVEM A,ERR1
	MOVE A,CPTR
	MOVEM A,ERR2
	JRST GO

CNTRUP:	PUSHJ P,RCH
	MOVE A,CH
	JRST VALRET
QUESTN:	MOVSI A,(JRST)
	TRCE FF,TRACEF
	MOVSI A,(POPJ P,)
	HLLM A,TRACS
	JRST RET

IFN TS,[
CONSET:	TRNN FF,ARG
	TYPR2 (SIXBIT /WNA/)
	JUMPL B,GCLOSE
	DPB B,[300,,CONOP]
	LSH B,-3
	DPB B,[60300,,CONOP]
	TSOPEN DCHN,CONOP
	MOVEI B,9.
	MOVEM B,NLINES
	SETOM GETTY
	JRST RET

GCLOSE:	TSOPEN DCHN,OTTY
	MOVE A,RGETTY
	MOVEM A,GETTY
	JRST RET

CONOP:	5,,(SIXBIT /T00/)
	SIXBIT /.TECO./
	SIXBIT /DISOUT/
]

BAKSL:	TRZE FF,ARG
	JRST BAKS1A
	MOVE IN,PT
	PUSHJ P,GETINC
	CAIE CH,"-
	JRST BAKSL7
	TRO FF,ARG
	JRST BAKSL6
BAKSLA:	PUSHJ P,GETINC
BAKSL7:	CAMLE IN,Z
	JRST BAKSL3
BAKSL6:	CAIN CH,".
	JRST BAKSL5
	CAIG CH,"9
	CAIGE CH,"0
	SOJA IN,BAKSL2
	MOVE A,SYL
	IMULI A,10.
	ADDI A,-60(CH)
	MOVEM A,SYL
	MOVE A,OSYL
	IMULI A,10
	ADDI A,-60(CH)
	MOVEM A,OSYL
	JRST BAKSLA
BAKSL5:	MOVE A,OSYL
	MOVEM A,SYL
	JRST BAKSL2
BAKSL3:	MOVE IN,Z
BAKSL2:	TRZE FF,ARG
	MOVNS SYL
	MOVEM IN,PT
	JRST CD7

BAKS1A:	MOVEI TT,40
	HRRM TT,DPT5
	MOVE TT,C
	TRZE FF,ARG2
	SKIPA F,CRDPT
BAKSL1:	MOVEI F,DPT
	MOVE T,[(700)BAKTAB-1]
	MOVEI C,0
	MOVEI CH,BAKSL4
	HRRM CH,LISTF5
	PUSHJ P,(F)
	MOVEI A,GLITCH
	IDPB A,T
	MOVE B,[(700)BAKTAB-1]
	PUSHJ P,NROOM
	PUSHJ P,INS1B
	JRST RET

BAKSL4:	IDPB CH,T
	AOJA C,CPOPJ

CLOSEB:	SKIPA C,[POP PF,]
OPENB:	MOVSI C,(PUSH PF,)
	PUSHJ P,QREGA
	HRRI C,QTAB-"0(CH)
	XCT C
	JRST CD5

SEMICL:	TRNN FF,ITERF
	TYPR2 (SIXBIT \SNI\)	;SEMICOLON NOT INSIDE ITERATION
	TRNN FF,ARG
	MOVE B,SFINDF

INCMA:	JUMPL B,CD
	MOVEI A,0
INCMA1:	PUSHJ P,SKRCH1
	CAIN CH,74
	AOJA A,INCMA1
	CAIE CH,76
	JRST INCMA1
	SOJGE A,INCMA1
INCMA2:	SOS INTDPH
	SUB P,[3,,3]
	POP P,ITERCT
	JRST RET

GRTH:	SKIPG INTDPH
	TYPR2 2,(SIXBIT \UMC\)	;UNMATCHED CLOSE ANGLE BRACKET
	SOSN ITERCT
	JRST INCMA2
	MOVE A,(P)
	MOVEM A,COMCNT
	MOVE A,-1(P)
	MOVEM A,CPTR
	TRNE FF,TRACEF
	PUSHJ P,CRR


LSSTH1:	TROA FF,ITERF
FLDSZ:	MOVEM B,NLINES
	JRST RET

LSSTH:	AOS INTDPH
	PUSH P,ITERCT
	PUSH P,COMAX
	PUSH P,CPTR
	PUSH P,COMCNT
	SETOM ITERCT
	TRZE FF,ARG
	MOVEM B,ITERCT
	SKIPE ITERCT
	JRST LSSTH1
	JRST INCMA1-1

LINE:	TRNE FF,ARG2
	TYPR2 5,(SIXBIT \WNA\)	;WRONG NO OF ARGS
	PUSHJ P,GETARG
	XOR B,C
	XORM B,PT
	JRST RET

IFE TS,[DECDMP:	PUSHJ P,IOWAIT
	CONO 435550
	MOVE A,CPTR
	ILDB CH,A
	CAIN CH,ALTMOD
	JRST MACDMP
	MOVE T,[440700,,IFE MOBY,[37760
]IFN MOBY,[40000+MOBY
]]	MOVEM T,MACCR
DCDMP2:	PUSHJ P,RCH
	CAIN CH,"$
	MOVEI CH,375
	IDPB CH,T
	CAIE CH,ALTMOD
	JRST DCDMP2
	MOVEI CH,15
	DPB CH,T
	MOVEI CH,0
	IDPB CH,T
	JRST MACDMP+1

IOWAIT:
IFN LPTR,	PUSHJ P,LPTWAT"
	PUSHJ P,UWAIT"
	CONSZ PTP,7
	JRST IOWAIT
TTYWAT:	CONSO TTY,30
	CONSZ TTY,30
	JRST TTYWAT
	POPJ P,

ADDT:	HLRZ A,DDT
	CAIE A,(CLEARM)
	JRST TAB
	PUSHJ P,IOWAIT
	JRST DDT
W=PUSHJ P,IOWAIT	;WAIT FOR IO WITH W$X
]
R=POPJ P,	;RETURN FROM DDT WITH R$X

IFN TS,[
DECDMP:	MOVE A,CPTR
	ILDB CH,A
	CAIE CH,ALTMOD
	JRST VCOM
	TSCLOSE TYIC,
	TSCLOSE TYOC,
	TSCLOSE UTYIC,
	TSCLOSE UTYOC,
	TSCLOSE LPTC,
	TSCLOSE DCHN,
	SKIPN GETTY
	DFLUSH
	TSVALRET
	JRST GO

VCOM:	MOVE T,[440700,,GCTAB]
VCOM2:	PUSHJ P,RCH
	CAIN CH,ALTMOD
	JRST VCOMX
	CAIN CH,"$
	MOVEI CH,ALTMOD
	IDPB CH,T
	JRST VCOM2

VCOMX:	MOVEI CH,EOFCHR
	IDPB CH,T
	TSVALRET GCTAB
	JRST RET
]
MAC:	PUSHJ P,QREGA
	TLNE A,400000
	TLNE A,377777
	JRST VALRET
	ADD A,QRBUF
	HRRZ IN,A
	PUSHJ P,GETINC
	CAIE CH,GLITCH
	JRST CD5
	PUSH P,COMAX
	PUSH P,CPTR
	PUSH P,COMCNT
	PUSH P,MARG1'
	PUSH P,MARG2'
	MOVE B,NUM
	TRNE FF,SYLF
	XCT DLIM
	TRNN FF,ARG
	MOVEI B,0
	MOVEM B,MARG2
	MOVE B,SARG
	TRNN FF,ARG2
	MOVEI B,0
	MOVEM B,MARG1	;SIGH
	PUSHJ P,GETINC
	MOVE A,CH
	PUSHJ P,GET
	ROT CH,7
	IOR A,CH
	SUBI A,3
	MOVEM A,COMCNT
	MOVEM A,COMAX
	ADDI IN,CBUF*5
	IDIVI IN,5
	MOVE OUT,BTAB(OUT)
	HRR OUT,IN
	TLZ OUT,17
	MOVEM OUT,CPTR
	JRST CD5


X:	PUSHJ P,QREGA
	PUSH P,CH
	CLEARM QTAB-"0(CH)
	PUSHJ P,GETARG
	CAMLE C,B
	TYPR2 (SIXBIT \2<1\)
	EXCH B,C
	SUBI C,-3(B)	;C HAS NO. CHARS TO X AWAY + 3
	ADD B,C	;1ST THREE CHARS ARE GLITCH, REMAINDER, LENGTH STRING/128.
	PUSH P,PT
	ADDM C,(P)
	MOVE D,BEG
	MOVEM D,PT
	PUSHJ P,NROOM
	MOVE OUT,RREL
	ADDM OUT,(P)
	ADD B,OUT
	MOVE OUT,BEG
	ADDM C,BEG
	MOVEI CH,GLITCH
	PUSHJ P,PUTINC
	MOVE CH,C
	PUSHJ P,PUTINC
	ROT CH,-7
	MOVE IN,B
X1:	PUSHJ P,PUTINC
	CAIN C,3
	JRST X2
	PUSHJ P,GETINC
	SOJA C,X1
X2:	MOVE B,PT
	SUB B,QRBUF
	TLO B,400000
	POP P,PT
	POP P,CH
	JRST USEA+1

IFN TS,[

COREX:	MOVE A,MEMSIZ
	ADDI A,CBUF*5
	PUSH P,AA
	IDIVI A,12000
	POP P,AA
	AOS A
	.CORE (A)
	POPJ P,
	AOS (P)
COREX3:	IMULI A,12000
	SUBI A,CBUF*5
	MOVEM A,MEMSIZ
	POPJ P,
]
QGET:	PUSHJ P,QREGA
	MOVE B,A
	TLZN B,377777
	TLZN B,400000
	JRST BAKSL1
	ADD B,QRBUF
	MOVE IN,B
	MOVE B,CH
	PUSHJ P,GETINC
	CAIE CH,GLITCH
	JRST RET
	PUSHJ P,GETINC
	MOVEM CH,C
	PUSHJ P,GETINC
	ROT CH,7
	IORM CH,C
	SUBI C,3
	PUSHJ P,NROOM
	MOVE OUT,PT
	HRRZ IN,QTAB-"0(B)
	ADD IN,QRBUF
	ADDI IN,3
QGET1:	JUMPE C,RET
	PUSHJ P,GETINC
	PUSHJ P,PUT
	AOS OUT,PT
	SOJA C,QGET1

IFE TS,[CLRBF:	PUSHJ P,PIBUFC
	CONO PTR,0
	JRST RET]

GETARG:	TRNE FF,ARG2
	JRST GETAG6
	TRON FF,ARG
	PUSHJ P,CHK2+2
GETAG7:	MOVE IN,PT
GETAG4:	JUMPLE B,GETAG2
	CAMN IN,Z
	JRST GETAG1
	PUSHJ P,GETINC
	CAIE CH,12
	JRST GETAG4
	SOJG B,GETAG4

GETAG1:	TRZE FF,COLONF
	SUBI IN,2
	CAMG IN,BEG
	MOVE IN,BEG
	MOVE C,PT
	MOVE B,IN
	TLZE FF,NEGF
	EXCH B,C
	POPJ P,

GETAG6:	ADD B,BEG
	ADD C,BEG
	POPJ P,


GETAG2:	SOS IN
	CAMG IN,BEG
	JRST GETAG3
	PUSHJ P,GETINC
	CAIE CH,12
	SOJA IN,GETAG2
	AOJLE B,.-1
GETAG3:	TLO FF,NEGF
	JRST GETAG1

DEFINE NTS A/
IFE TS,	A
IFN TS,	TYPR2 (SIXBIT \NTS\)
TERMIN

DEFINE OTS A/
IFE TS,	TYPR2 (SIXBIT \OTS\)
IFN TS,	A
TERMIN

ECMD:IFE TS,PUSHJ P,.OPNTP
ECMD1:IFN TS,[PUSHJ P,GDEV
	TLO FF,CTLUF
]	PUSHJ P,LRCH
IFN TS,	CAIE CH,"Y
	CAIN CH,"L
	JRST LISTF
	CAIN CH,"X
	JRST EXIT
	CAIN CH,"E
	JRST EEND
	CAIN CH,"R
	JRST .OPNRD
	CAIN CH,"C
	NTS JRST CLSTP
	CAIN CH,"F
	JRST .FILE
	CAIN CH,"D
	JRST DELE
	CAIN CH,"N
	JRST RENAM
	CAIN CH,"I
	JRST WINIT
	CAIN CH,"K
	JRST TAPKIL"
	CAIN CH,"S
	NTS JRST MTNAM
	CAIN CH,"T
IFE TS,JRST CNTRU1
IFN TS,POPJ P,
IFN TS,	CAIE CH,"Z
	CAIN CH,"M
	JRST LISTFM
	CAIN CH,"P
	JRST BPNTRD
IFN TS,[CAIN CH,"W
	JRST WWINIT
	CAIN CH,"A
	JRST EASSIGN
	CAIN CH,"U
	JRST EDESINE
	CAIN CH,^U
	JRST EUHACK
]	TYPR2 (SIXBIT /IUC/)

EIDEV:	-1	;CURRENT DEV SAVED BY EI & EW FOR EF TO RESTORE
ERDEV:	-1	;CURRENT READ DEV

IFE TS,[.OPN3:	TLO FF,CTLUF
.OPNTP:	MOVE A,B
	TRNN FF,ARG
.OPN2:	MOVE A,UFPNTR+2
.OPN1:	PUSHJ P,FILEST"
	JRST UTERR
	POPJ P,

UTERR:	JRST .+3(A)
	TYPR1 [ASCII /UNIT UNABLE
!/]
	TYPR1 [ASCII /BAD DIRECTORY
!/]
	TYPR1 [ASCII /TOO MANY DIRECTORIES
!/]
]

IFN TS,[GDEV:	TRNN FF,ARG
	POPJ P,
	MOVMS B
	IDIVI B,NDEVS
	MOVE B,DEVNAM(E)
	HLRM B,UTF
	POPJ P,

DEVNAM:	IRPS XX,,DSK UT1 UT2 UT3 UT4 DD0 DD1
	SIXBIT \XX\
TERMIN
NDEVS==.-DEVNAM

ASLEEP:	TRZE FF,ARG
	SLEEP B,
	POPJ P,

EASSIGN:	LDB B,[300,,UTF]
	.ASSIGN B,
	TYPR2 (SIXBIT /UNA/)
	POPJ P,

EDESINE:	LDB B,[300,,UTF]
	.DESIGN B,
	TYPR2 (SIXBIT /UNA/)
	POPJ P,
]

BPNTRD:	PUSHJ P,.OPNRD
	TRZ FF,ARG
	JRST .FNPNT

IFN TS,[.OPNRD:	PUSHJ P,FFRRDD
RRED:	HRRZ A,UTF
	HRRZM A,ERDEV
	MOVEI A,2
	HRLM A,UTF
	TSOPEN UTYIC,UTF
	MOVEI CH,UTYIC
	.EOFC CH,
	MOVEM CH,SYSEOF
	DPB CH,[350700,,UTIBE]
	TLO FF,UREAD
	MOVE CH,[700,,UTIBE-1]
	MOVEM CH,UTYIP
IFN HLSTSW,SETOM UROSBF
	POPJ P,

EEND:	TROA FF,ALTF
EXIT:	TRZ FF,ALTF
	HRLOI B,377777	;INFINITY
	TRO FF,ARG
	TLNN FF,UREAD
	PUSHJ P,PUNCHR
	TLNE FF,UREAD
	PUSHJ P,PUNCH
	TRNE FF,ALTF
	JRST .FILE
	MOVE A,50
	MOVE AA,52
	PUSHJ P,.FILE1
	SKIPN A,53
	JRST EXIT1
	MOVEM A,UTF2
	MOVE A,50
	MOVEM A,UTF1
	PUSHJ P,DELE1
EXIT1:	TRZN FF,COLONF
	.VALUE [ASCII \.0<MIDASDEBUGJL\+3_10]
	.VALUE [ASCII \0<MIDASDEBUGJL\+3_26]
	TRZ FF,ARG
	SKIPE A,52
	MOVEM A,UTF2
	JRST SYMLPT

UCLOSE:	MOVE A,UTOF+1
	SKIPA AA,[SIXBIT /FLAP/]
.FILE:	PUSHJ P,FRD
.FILE1:	MOVEM A,UTF3
	MOVEM AA,UTF4
	SKIPGE CH,EIDEV
	TYPR2 (SIXBIT /NFO/)
	HRRM CH,UTF
	TDZA E,E
.FILEX:	MOVEI E,1	;DON'T RENAME
IFN HLSTSW,[MOVEI AA,2
.FILEY:]MOVEI CH,EOFCHR
	PUSHJ P,UTYO
	MOVEI CH,14
IFN HLSTSW,[PUSHJ P,UTYO
	SOJG AA,.FILEY
].FILEA:PUSHJ P,UTYO
	MOVE CH,UTYOP
	HRRI CH,EOFCHR
	TLNE CH,760000
	JRST .FILEA
IFN HLSTSW,[.FILB:SKIPL SEMMOD
	JRST .FILEB
	MOVE A,SECTP
	CAIE A,SECT-1
.FILB6:	SKIPN UROSBF
	JRST .FILB5
	PUSHJ P,UTYI
	JRST .FILB6

.FILB5:	MOVE A,SECTP
	CAIL A,SECTPE
	TYPR2 (SIXBIT \TMS\)
	PUSH A,SECNAM
	PUSH A,CHKSUM
	HRRZM A,SECTP
	PUSH P,B
	PUSH P,C
	MOVEI C,1
	MOVEI A,SECT-1
.FILB9:	CAML A,SECTP
	JRST .FILB3
	LDB AA,[350700,,1(A)]
	CAIN AA,";
	JRST .FILB2
	MOVE AA,2(A)	;CKSM
	MOVEI B,OSECT-3
.FILB4:	ADDI B,2
	CAML B,OSECTP
	JRST .FILB2
	MOVE CH,1(A)
	XOR CH,1(B)
	TDNE CH,[-2]
	JRST .FILB4
	CAME AA,2(B)
.FILB2:	IORM C,1(A)
	ADDI A,2
	JRST .FILB9

.FILB3:	POP P,C
	POP P,B
	MOVEI A,0
	MOVE CH,SECTP
	SUBI CH,SECT-1
	PUSHJ P,UWO
	MOVEM CH,SECTE
.FILB1:	CAML A,SECTE
	JRST .FILEB
	MOVE CH,SECT(A)
	PUSHJ P,UWO
	AOJA A,.FILB1

.FILEB:	MOVEI A,SECT-1
	MOVEM A,SECTP
	MOVEI A,OSECT-1
	MOVEM A,OSECTP
]	MOVEI CH,UTOBUF-1
	SUB CH,UTYOP
	HRLS CH
	HRRI CH,UTOBUF
	IOT UTYOC,CH
	JUMPN E,.FILE2
	SETZM UTF1
	MOVEI CH,UTYOC
	MOVEM CH,UTF2
	FDELE UTF
	JRST OPNER1
	MOVE A,UTF3
	MOVEM A,UTF1
	MOVE A,UTF4
	MOVEM A,UTF2
.FILE2:	PUSH P,0
	MOVE 0,[REPEAT 5,[EOFCHR_<7*.RPCNT>+]]	;DISGUST
	TSCLOSE UTYOC,
	SETOM EIDEV
	POP P,0
	TLZ FF,UWRITE
	POPJ P,


WWINIT:	PUSHJ P,FFRDEV
WINIT:	MOVSI A,3
	HRR A,UTF
	HRRZM A,EIDEV
	JSP T,WINITA
	JSP TT,OPNER
IFN HLSTSW,[SETZM CHKSUM
	MOVE A,[";_35]
	MOVEM A,SECNAM
	MOVEI A,SECT-1
	MOVEM A,SECTP
	TRZ FF,DUBSCF
]	POPJ P,

WINITA:	MOVEM A,UTOF
	STOPEN UTYOC,UTOF
	XCT (T)
FHAK:	TLO FF,UWRITE+PNSTOP
	MOVE CH,[700,,UTOBUF-1]
	MOVEM CH,UTYOP
	MOVNI CH,<UTOBE-UTOBUF>*5
	MOVEM CH,UTYOCT
	JRST 1(T)

CNTRLE:	TSCLOSE LPTC,
	TLZ FF,LPTF
	JRST RET

CNTRLB:	TSOPEN LPTC,TSLPTF
]
IFE TS,[CNTRLB:]
	MOVEI CH,14
	AOSN CTLBF'
IFE TS-LPTR+1,	PUSHJ P,APILPT
IFN TS,	.IOT LPTC,CH
	TLO FF,PNSTOP+LPTF
	JRST CD

PATCH:	BLOCK 40

IFE TS,[ZZ==.
RDCHN==3ооPCHCHN==UTCCHN"
FLGCHN==6
DATCHN==7
APRCHN==5
TPCHN==2
TPCHNA==40
LPTCHN==PCHCHN
TTYCHN==PCHCHN


LOC 40+APRCHN+APRCHN
JSR IAPRBRK

LOC 40+RDCHN+RDCHN
JSR RBRK

LOC 40+FLGCHN+FLGCHN
JSR RECYC

LOC 40+DATCHN+DATCHN
BLKO DIS,BLKOP
TYPR1 [ASCII /DISPLAY BLKO OVERFLOW
/]

LOC 40+PCHCHN+PCHCHN
JSR PBRK

LOC ZZ

RECYC:	0
	EXCH A,BLKOPR
	MOVEM A,BLKOP
	EXCH A,BLKOPR
	CONO DIS,300+FLGCHN*10+DATCHN
	JRST 12,@RECYC
]DISINI:IFN TS,[SKIPE GETTY
	JRST DIS1
	.DSTOP	;AVOID RANDOMNESS ALSO MPVS
]	MOVE T,Z
	IDIVI T,5
	ADDI T,CBUF+2
	MOVEM T,DISBUF
	MOVEI TT,20115+IFN TS,2
	IOR TT,CHSIZ
	PUSH T,TT
	PUSH T,[221700060000
	HRLI T,600
	MOVEM T,DISPNR
	LDB TT,[(40200)CHSIZ
	MOVNS TT
	MOVEI T,170.
	LSH T,(TT)
	TRZ T,7
	MOVEM T,CHCNTS
	MOVNM T,CHCNT
	MOVEI T,100.
	MOVEM T,LINES
IFE TS,	CLEARM BARPNT
	MOVEI T,(HRRZ A,(CH))
	HRLM T,DISA1
	POPJ P,

IFN TS,[
MGEL==22.	;NUMBER LINES IN GE CONSOLE
MGEC==45.	;#CHARS/LINE
DIS1:	HRLI CH,-MGEL-1
	HLROM CH,LINES'
	IOT DCHN,[^T]
DIS1A:	AOS LINES
	HRLI CH,-MGEC
	HLROM CH,CRCNT'
	POPJ P,

DIS2:	PUSH P,CH
	MOVEI CH,[566660,,]	;CLOSE BRAK, OPEN BRAK
	CAIN IN,1(A)
	PUSHJ P,DIS7
	POP P,CH
	JRST .+2
DIS3:	TRO FF,VIEWF
	SKIPG LINES
	CAIN CH,12
	POPJ P,
	SKIPL LINES
	JRST DIS4
	CAIN CH,15
	JRST DIS6A
	CAIN CH,11
	JRST DIS8
	CAIE CH,ALTMOD
	CAIL CH,40
	JRST DIS6
	DPB CH,[260600,,DIS3A]
	SKIPA CH,DIS6B
DIS5:	MOVEI CH,[ASCIZ \FLUSHED\]
DIS7:	HRLI CH,440700
	MOVEM CH,DIS7A'
DIS7B:	ILDB CH,DIS7A
	JUMPE CH,CPOPJ
	PUSHJ P,DIS6
	JRST DIS7B

DIS6:	AOSL CRCNT
DIS6A:	PUSHJ P,DIS1A
	IOT DCHN,CH
DIS6B:	POPJ P,DIS3A

DIS4:	AOS LINES
	HRRM CH,DIS3A
	MOVEI CH,[ASCIZ \--MORE--\]
	PUSHJ P,DIS7
	.LISTE CH,
	JUMPN CH,DIS5
	PUSHJ P,TYI
	CAIN CH,40
	JRST DIS4A
	HRRM CH,UNRCHC
	SETOM UNRCHF
	JRST DIS5

DIS4A:	PUSHJ P,DIS1
	HRRZ CH,DIS3A
	JRST DIS3

DIS3A:	ASCII \_@\

DIS8:	MOVEM PF,DIS7A
	PUSHJ P,DIS6
	MOVN CH,CRCNT
	SUBI CH,5
	IDIVI CH,8
	IMULI CH,8
	ADDI CH,5
	MOVNM CH,CRCNT
	MOVE PF,DIS7A
	POPJ P,

DEFINE LC A
A,,DISLC
TERMIN

DEFINE UC A
DISUC,,A
TERMIN

DEFINE AC A
A,,A
TERMIN


DISAD:	MOVE A,PT
IFN TS,[SKIPE GETTY
	JRST DIS2
]	SKIPN LINES
	POPJ P,
	CAIN IN,1(A)
	JRST DISBAR
DISA1:	HRRZ\HLRZ A,DISTBL(CH)
	TRNN A,-100
	JRST DISA2
	TRNE A,-200
	JRST (A)
	MOVEI A,40
	PUSHJ P,DISA2
	XCT DISA1
	SKIPLE CHCNT
DISA2:	AOSG CHCNT
DISA3:	IDPB A,DISPNR
	POPJ P,

DISACR:	MOVE CH,CHCNTS
	MOVNM CH,CHCNT
	MOVEI A,34
	JRST DISA3

DISALF:	SOS LINES'
	MOVEI A,33
	JRST DISA3

DSATB:	MOVEI A,40
DISAT1:	LDB CH,[300,,CHCNT]
	CAIE CH,7
	PUSH P,[DISAT1]
	JRST DISA2

DISBLB:	MOVEI A,50
	PUSHJ P,DISA2
	TRO CH,100
DISUC:	SKIPA A,[HRRZ A,35(CH)]
DISLC:	MOVE A,[HLRZ A,36(CH)]
	IDPB A,DISPNR
	HLLM A,DISA1
	JRST DISA1

DISBS:	SOS CHCNT
	MOVEI A,72
	JRST DISA3

DISRUB:	MOVEI A,"O&77
	PUSHJ P,DISA2
	MOVEI CH,10
	PUSHJ P,DISA1
	MOVEI A,"X&77
	JRST DISA2


DISBAR:IFE TS,[	MOVEI A,36
	IDPB A,DISPNR
	MOVEI A,62
	IDPB A,DISPNR
	MOVE A,DISPNR
	MOVEM A,BARPNT
]IFN TS,[	MOVEI A,36
	IDPB A,DISPNR
	MOVEI A,61
	IDPB A,DISPNR
	MOVEI A,72
	IDPB A,DISPNR
	MOVEI A,62
	IDPB A,DISPNR
	MOVEI A,72
	IDPB A,DISPNR
	MOVEI A,60
	IDPB A,DISPNR
]	MOVEI A,(HLRZ A,(CH))
	HRLM A,DISA1
	JRST DISA1

DISTBL:	REPEAT 7,LC DISBLB
	LC 63
	LC DISBS
	AC DSATB
	AC DISALF
	LC DISBLB
	LC DISBLB
	AC DISACR
	REPEAT 33-16,LC DISBLB
	LC 47
	REPEAT 4,LC DISBLB
	AC 40
	REPEAT 47-41,UC <.-DISTBL>&77
	LC 100+65,	;SINGLE QUOTE
	REPEAT 133-50,UC <.-DISTBL>&77
	LC 53
	LC 52
	LC 54
	LC 100+67
	LC 60
	LC 100+66
	REPEAT 173-141,LC <.-DISTBL-140>
	LC 55
	LC 62,	;VERTICAL BAR=174
	LC 56
	LC 43,	;TILDE=176
	UC DISRUB
IFN .-DISTBL-200,PRINTX /DISTBL LOSS/

DISCLG:	IFN TS,[
	SKIPE GETTY
	JRST DISCL1
]	MOVE A,DISPNR
	MOVEI CH,37
	IDPB CH,A
	TLNE A,550000
	JRST .-2
	TLC A,2400
	MOVEI CH,3000
	IDPB CH,A
	HRRZ B,DISBUF
	MOVEM B,BLKOPR
IFN TS,[	CLEARM 6(A)	;MAKE SURE ENOUGH MEMORY FOR DISPLAY
	SUB A,DISBUF
	MOVNI A,4(A)
	HRLM A,BLKOPR
	SKIPGE DSW
	DFLUSH
	SKIPL DSW'
	DSTART BLKOPR
	JFCL
]IFE TS,[MOVEI CH,6
	CAIL B,-20.(A)
	XORM CH,1(B)
	CONO PI,2237
	CONO PI,4002
	CONO APR,2000+APRCHN
]	POPJ P,
IFN TS,[DISCL1:	IOT DCHN,[15]
	AOSG LINES
	JRST DISCL1
	JRST DIS1
]
GET:	MOVE TT,IN
	IDIVI TT,5
	LDB CH,BTAB(TT1)
	POPJ P,

PUT:	MOVE TT,OUT
	IDIVI TT,5
	DPB CH,BTAB(TT1)
	POPJ P,

GETINC:	PUSHJ P,GET
	AOJA IN,CPOPJ

PUTINC:	PUSHJ P,PUT
	AOJA OUT,CPOPJ

BTAB:	350700+TT,,CBUF
	260700+TT,,CBUF
	170700+TT,,CBUF
	100700+TT,,CBUF
	10700+TT,,CBUF


NROOM:	CLEARM CRREL
	CLEARM RREL
	MOVEM 17,AC2+15
	MOVE 17,PT
	CAME 17,Z
	JRST NROOM7
	ADD 17,C
	CAMGE 17,MEMSIZ
	JRST NROOM1
NROOM7:	MOVE 17,[(2)AC2]
	BLT 17,AC2+14
	JUMPL C,NROOM6
	SETOM GCFLG
NROOM9:	MOVE 17,Z
	ADD 17,C
	CAML 17,MEMSIZ
	JRST NRGC
	MOVE A,PT
	CAMN A,Z
	JRST NRM5A
	ADDI 17,CBUF*5
	MOVE 14,C
	IDIVI 14,5
	IMULI 15,7
	MOVN 13,15
	MOVEI 15,-43(15)
	MOVE 11,PT
	ADDI 11,CBUF*5
	IDIVI 11,5
	MOVNI 16,-5(12)
	IMULI 16,7
	DPB 16,[(300600)NROOM2]
	ADDI 14,1(11)
	MOVE 16,Z
	ADDI 16,CBUF*5
	IDIVI 16,5
	MOVEI B,1(16)
	SUB B,11
	HRLI 11,(MOVE A,(B))
	HRLOI 12,(ROT A,)
	HRLI 13,(ROTC A,)
	HRLI 14,(MOVEM AA,(B))
	HRLI 15,(ROTC A,)
	MOVE 17,[JRST .+3]
	MOVE 16,.+1
	SOJGE B,11
	ROTC A,43(13)
	DPB A,NROOM2
NRM5A:	ADDM C,Z
NROOM5:	MOVS 17,[(2)AC2]
	BLT 17,17
	POPJ P,

IFN TS,[
GCRM:	PUSHJ P,COREX
	TYPR2 (SIXBIT /SCE/)
	POPJ P,
]
NRGC:	PUSHJ P,GC
	JRST NROOM9

NROOM2:	(10000),-1(14)

NROOM1:	ADDM C,Z
	MOVE 17,AC2+15
	POPJ P,

NROOM6:	MOVE 14,PT
	ADDI 14,CBUF*5
	IDIVI 14,5
	MOVEM 14,B
	HRRM 14,NROOM4
	IMULI 15,7
	DPB 15,[(300600)NROOM4
	MOVNI 15,-44(15)
	DPB 15,[(360600)NROOM4
	MOVE 11,Z
	ADDI 11,CBUF*5
	IDIVI 11,5
	ADDI 11,1
	MOVE 13,C
	IDIVI 13,5
	ADDI 13,-1(11)
	MOVNM 14,12
	IMULI 12,7
	MOVNI 15,-43(12)
	SUBI B,1(13)

NROOM8:	HRLI 11,(MOVE AA,(B))
	HRLI 12,(ROTC A,)
	HRLI 13,(MOVEM A,(B))
	MOVE 14,[ADDM A,@13
	HRLI 15,(ROTC A,)
	MOVE 17,[JRST NROOM3
	ADDM C,Z
	LDB C,NROOM4
	MOVE A,@11
	ROT A,-1
	MOVE 16,.+1
	AOJLE B,11

NROOM3:	DPB C,NROOM4
	JRST NROOM5

NROOM4:	0

GC:	AOSE GCFLG
IFN TS,	JRST GCRM
IFE TS,	TYPR2 (SIXBIT /SCE/)
GCC:	SETOM GCPTR
	CLEARM SYMS
	MOVE T,[SYMS,,SYMS+1
	BLT T,SYMEND-1	;CLEAR O SYM TAB
	MOVEI T,CPTR	;MARK CPTR
	PUSHJ P,GCMA
	HRRZ T,P
	CAIL T,PDL
	PUSHJ P,GCMA
	CAILE T,PDL
	SOJA T,.-2	;MARK PDL
	HRRZ T,AC2+PF-2	;SAVED PF
	CAIL T,PFL
	PUSHJ P,GCM
	CAILE T,PFL
	SOJA T,.-2
	MOVE T,[(,-NQREG)QTAB]
	PUSHJ P,GCM
	AOBJN T,.-1
	SKIPGE GCPTR
	JRST GCE

GCS:	MOVE IN,QRBUF
GCS1A:	MOVSI TT,1*5
	MOVE OUT,GCPTR
GCS1:	HRRZ A,GCTAB(OUT)
	ADD A,QRBUF
	CAMGE A,IN
	JRST GCS2
	CAMGE A,TT
	MOVE TT,A
GCS2:	SOJGE OUT,GCS1
	TRNN TT,-1
	JRST GCE1
	MOVE F,TT
	IDIVI IN,5
	IDIVI F,5
	ADDI IN,CBUF+1
	ADDI F,CBUF
	MOVS OUT,F
	MOVE T,F
	SUB T,IN
	JUMPL T,GCS4A
	HRR OUT,IN
	MOVE B,Z
	IDIVI B,5
	SUBI B,-CBUF(T)
	BLT OUT,(B)
	MOVNS OUT,T
	IMULI OUT,5
	ADDM OUT,BEG
	ADDM OUT,PT
	ADDM OUT,Z
	ADDM OUT,RREL
	MOVE CH,GCPTR
GCS3:	HRRZ A,GCTAB(CH)
	ADD A,QRBUF
	CAMGE A,TT
	JRST GCS4
	ADDM OUT,GCTAB(CH)
	HLRZ A,GCTAB(CH)
	CAIN A,CPTR
	ADDM OUT,CRREL
	SKIPL (A)
	ADDM T,(A)
	SKIPGE (A)
	ADDM OUT,(A)
GCS4:	SOJGE CH,GCS3
	ADD TT,OUT
GCS4A:	MOVE IN,TT
	PUSHJ P,GETINC
	CAIE CH,GLITCH
GCERR:	TYPR1 [ASCII /GC ERROR
!/]
	PUSHJ P,GETINC
	MOVE A,CH
	PUSHJ P,GETINC
	ROT CH,7
	IOR A,CH
	ADDI IN,-3(A)
	JRST GCS1A

GCM:	MOVE IN,(T)
	TLZE IN,400000
	TLZE IN,377777
	POPJ P,
	ADD IN,QRBUF
GCM2:	CAML IN,BEG
	POPJ P,
	PUSHJ P,GET
	CAIE CH,GLITCH
	POPJ P,
	SUB IN,QRBUF
	JUMPL IN,CPOPJ
	AOS TT,GCPTR
	CAIL TT,GCTBL
	JRST GCERR
	HRL IN,T
	MOVEM IN,GCTAB(TT)
	POPJ P,

GCMA:	LDB TT,[(221400+T)]	;PTR IN T
	CAIE TT,700
	POPJ P,
	MOVE IN,-1(T)	;GET COMAX
	SUB IN,1(T)	;SUB COMCNT
	LDB TT,[(360600+T)]
	IDIVI TT,7
	HRRZ TT1,(T)
	IMULI TT1,5
	SUBI TT1,CBUF*5-2(TT)
	SUBM TT1,IN
	JRST GCM2

GCE:	MOVE IN,QRBUF
GCE1:	IDIVI IN,5
	ADDI IN,1
	MOVE T,BEG
	IDIVI T,5
	CAMG T,IN
	POPJ P,
	SUBM IN,T
	ADDI IN,CBUF
	HRLS IN
	SUB IN,T
	MOVSS IN
	MOVE B,Z
	IDIVI B,5
	ADDI B,CBUF(T)
	BLT IN,(B)
	IMULI T,5
	ADDM T,BEG
	ADDM T,PT
	ADDM T,Z
	ADDM T,RREL
	POPJ P,

IFE TS,[IAPRBRK:	0
	PUSH P,A
	CONSO 1200
	JRST ILMEM
	CONO 1440+APRCHN
	JSR APRBRK"
	SOSG BARFL1
	JRST BARFL2
APRBK7:	SKIPGE OFTAPE
	CONSZ PTR,400
	JRST APRB69
	SETOM PWOWOT
APRB69:	SKIPGE RBRK+1
	AOSG RDRUN
	JRST APRBK3
	MOVEI A,(JSR)
	HRLM A,RBRK+1
	SETOM OFTAPE
	CONO PTR,400
APRBK3:	CONSO DIS,77
	JRST APRBA
	MOVE A,BLKOP
	HRRZS BLKOP
	CAMN A,BLKOP
	CONO PI,4002
APRBA:	AOS A,TIME
	SUB A,TSTPTM
	CAIL A,10
	SKIPG BRKFLG
	JRST APRBA1
	SETOM BRKFLG
	HRROS STOPF
APRBA1:	POP P,A
	JRST 12,@IAPRBRK

BARFL2:	CONSO DIS,77
	JRST APRBK7
	MOVEI A,30.
	MOVEM A,BARFL1
	LDB A,BARPNT
	TRC A,22
	DPB A,BARPNT
	JRST APRBK7

ILMEM:	MOVEI B,0
	CONSO 200000
	MOVEI B,10
	CONO 433550+APRCHN
	MOVE P,[(,-LPDL)PDL]
	MOVEI CH,7
	PUSHJ P,TYO
	SOJG B,.-1
	TRO FF,VIEWF
	JRST 10,ERR+1
IFN LPTR,[
APILPT:	EXCH A,CH
	PUSHJ P,PILPT"
	EXCH A,CH
	POPJ P,
]

PIPUN:	SKIPG PUNCC
	JRST .-1
	IDPB CH,PUNIP
	HRRZ CH,PUNIP
	CAIN CH,PUNBE-1
	MOVEI CH,PUNBO
	HRRM CH,PUNIP
	SOS CH,PUNCC
	CONSO PTP,7
	CONO PTP,10+PCHCHN
	POPJ P,

PBRK:	0
	JSR UTCBRK"
	PUSH P,A
PBRK1:
IFN LPTR,[
	JRST LPTBRK"
LPTRTN":]
	CONSO PTP,10
	JRST PITELE
	MOVEI A,+<PUNBE-PUNBO>*4-4
	CAMG A,PUNCC
	JRST PUNSTP
	ILDB A,PUNOP
	DATAO PTP,A
	HRRZ A,PUNOP
	CAIN A,PUNBE-1
	MOVEI A,PUNBO
	HRRM A,PUNOP
	AOSA PUNCC
PUNSTP:	CONO PTP,0
POPRET":	POP P,A
	JRST 12,@PBRK

PUNCC:	+<PUNBE-PUNBO>*4-4
PUNIP:	(1000)PUNBO-1
PUNOP:	(1000)PUNBO-1
TTYRET=POPRET
]
IFN TS,[
TSINT:	0
	0
	MOVEM A,TSA'
	TLZ FF,FATAL
	EXCH B,TSINT
TSIL:	TRZE B,TYPIN
	JRST TSINT1
	TRZE B,MPV1
	JRST TSINT4
	TRZE B,PDLOV1
	JRST TSINT3
	TRZE B,DISMPV
	JRST TSINT6
	MOVE A,TSA
	MOVE B,TSINT
	TLNN FF,FATAL
	.DISMI TSINT+1
	.DISMI [GOX]

TSINT1:	ITYI A,
	JRST TSIL
	CAIN A,^Z
	.LOGOUT
	CAIE A,^G
	JRST TSIL
	RESET TYOC,
	RESET TYIC,
	SETOM STOPF
	HRRZ A,TSINT+1
	CAIN A,TYIW
TSINT5:	TLO FF,FATAL
	JRST TSIL

TSINT4:	SOS TSINT+1
	PUSHJ P,COREX
	JRST .+2
	JRST TSIL
	IITYPR [SIXBIT \NOMEM\]
TSIN4A:	MOVEI A,15.
	.SLEEP A,
	PUSHJ P,COREX
	JRST TSIN4A
	JRST TSIL

TSINT6:	IITYPR [SIXBIT /DISMPV/]
	JRST TSINT5

TSINT3:	ITYPR (SIXBIT \PCE\)
	JRST TSINT5
]
IFE TS,[RBRK:	0
	JSR RDTEM
	EXCH B,RDTEM
	PUSH P,A
	MOVNI A,10
	MOVEM A,RDRUN
	IDPB B,RDIP
	HRRZ A,RDIP
	CAIN A,RBEND-1
	MOVEI A,RBUF
	HRRM A,RDIP
	AOS A,RCC
	CAILE A,RDCHAR/4
	CAIE B,214
	CAIL A,RBEND*5-RBUF*5-30
	JRST RBRK2
RBRK3:	POP P,A
	MOVE B,RDTEM
	JRST 12,@RBRK

RBRK2:	MOVEI A,(JSR)
	HRLM A,RBRK+1
	JRST RBRK3

RDTEM:	0
	CONO PTR,RDCHN
	JRST 12,@RBRK
]
IFN 7SW,[
7RI:	TLZE FF,7W	;OR PATCH TO TLZA
	JRST 7RI1
7RI2:	MOVEI AA,0BIT+1BIT
	XORM AA,7ST
	TLON FF,7R
	CONO 760,@7ST
	JRST RET
7RI1:	MOVEI CH,176
	PUSHJ P,7WCH
	JRST 7RI2

7WI:	TRNE FF,ARG
	JUMPE A,7WI1
	TLNE FF,7R	;OR PATCH TO TLZA
	TYPR2 (SIXBIT \7WI\)
	MOVEI AA,1BIT
	TLON FF,7W
	PUSHJ P,7WCH3A
	JRST RET

7WI1:	MOVEI CH,176
	TLZE FF,7W+PNSTOP
	PUSHJ P,7WCH
	JRST RET
7RCH:	PUSH P,AA
	PUSH P,C
	MOVEI CH,0
7RCH1A:	CLEARM 7ERRF
	MOVEI C,200
7RCH1:	LSH C,-1
	JUMPE C,7RCH2
	PUSHJ P,7RCH3
	JUMPL AA,7RCH4
	TRNE AA,1BIT
	TDO CH,C
7RCH1B:	PUSHJ P,7RCH3A
	JRST 7RCH1

7RCH2:	PUSHJ P,7RCH3
	JUMPGE AA,7RCH5
7RCH2B:	PUSHJ P,7RCH3A
	PUSHJ P,7RCH3
	JUMPL AA,7RCH2A
	SKIPE 7ERRF
	TRC AA,0BIT+1BIT
	PUSHJ P,7RCH3A
	SKIPE 7ERRF
	JRST 7RCH1A
	CAIE CH,176
	JRST 7RCH7
	PUSHJ P,7RCH3
	JUMPGE AA,[TYPR2 (SIXBIT \7LZ\)]
	TLZ FF,7R
	MOVEI CH,14
	TRO FF,FINF
	JRST 7RCH7

7RCH4:	MOVEI AA,1BIT
7RCH5:	SETOM 7ERRF
	JRST 7RCH1B

7RCH2A:	SETOM 7ERRF
	MOVEI AA,1BIT
	JRST 7RCH2B


7RCH3:	CONI 760,AA
	ANDI AA,0BIT+1BIT
	XOR AA,7STA
	JUMPE AA,7RCH3
	CONI 760,AA
	ANDI AA,0BIT+1BIT
	XOR AA,7STA
	XORM AA,7STA
	CAIN AA,0BIT+1BIT
	SETOM AA
	POPJ P,

7RCH3A:	JUMPL AA,7RCH3B
	XORM AA,7ST
7RCH3C:	CONO 760,@7ST
	POPJ P,
7RCH3B:	MOVEI AA,0BIT+1BIT
	XORM AA,7ST
	SETOM AA
	JRST 7RCH3C

7WCH:	PUSH P,AA
	PUSH P,C
	PUSH P,B
7WCH1A:	CLEARM 7ERRF
	MOVEI C,200
7WCH1:	LSH C,-1
	JUMPE C,7WCH2
	MOVEI AA,0BIT
	TDNE CH,C
	MOVEI AA,1BIT
	PUSHJ P,7WCH3A
	JRST 7WCH1

7WCH2:	MOVEI AA,0BIT+1BIT
	PUSHJ P,7WCH3A
	MOVEI AA,0BIT
	SKIPE 7ERRF
	MOVEI AA,1BIT
	PUSHJ P,7WCH3A
	SKIPE 7ERRF
	JRST 7WCH1A
	POP P,B
7RCH7:	POP P,C
	POP P,AA
	POPJ P,

7WCH3A:	XORM AA,7ST
	CONO 760,@7ST
	HRRM AA,7WCH3B
7WCH3:	CONI 760,AA
	ANDI AA,0BIT+1BIT
	CAMN AA,7STA
	JRST 7WCH3
	CONI 760,AA
	ANDI AA,0BIT+1BIT
	XOR AA,7STA
	XORM AA,7STA
7WCH3B:	CAIE AA,.
	SETOM 7ERRF
	POPJ P,

7ST:	0
7STA:	0
7ERRF:	0
]
IFE TS,[
PIRPA3:	AOSN PWOWOT
	PUSHJ P,PIBUF1
	AOSG OFTAPE
	JRST PIBUFX
	MOVEI CH,20
	SKIPL RBRK+1
	PUSHJ P,RDST+1
PIRPA:	SKIPG RCC
	JRST PIRPA3
	HRRZ CH,RDOP
	CAIN CH,RBEND-1
	MOVEI CH,RBUF
	HRRM CH,RDOP
	SOS CH,RCC
	SKIPL OFTAPE
	CAIL CH,RDCHAR*3/4
	JRST PIRPA4
	SKIPL RBRK+1
	PUSHJ P,RDST
PIRPA4:	ILDB CH,RDOP
	CAIN CH,177
	JRST PIRPA
	JUMPE CH,PIRPA
	POPJ P,

PIBUFX:	CONO PTR,400
	JRST PIBUFD

PIBUF1:	CLEARM OFTAPE
PIBUFC:	CONO PTR,0
PIBUFD:	MOVNI CH,10
	MOVEM CH,RCC
	MOVE CH,RDIP
	MOVEM CH,RDOP
	MOVEI CH,(JSR)
	HRLM CH,RBRK+1
	MOVEI CH,14
CBELLS:	POPJ P,[ASCII \\]

	MOVEI CH,10
	MOVNM CH,RDRUN
	CONSZ PTR,20
	JRST .-1
	HRLI CH,(DATAI PTR,)
	HLLM CH,RBRK+1
	CONO PTR,RDCHN(CH)
	POPJ P,

RDOP:	(700)RBUF
RDIP:	(700)RBUF
RCC:	0
OFTAPE:	0
RDRUN:	0

RDST:	MOVEI CH,10
	MOVNM CH,RDRUN
	CONSZ PTR,20
	JRST .-1
	HRLI CH,(DATAI PTR,)
	HLLM CH,RBRK+1
	CONO PTR,RDCHN(CH)
	POPJ P,

LOOK:	HRLZI A,-27
	SKIPA TT,UFPNTR"
	ADDI TT,2
	CAMN B,(TT)
	CAME C,1(TT)
	AOBJN A,LOOK+2
	TLZN A,-1
	POPJ P,	;NOT FOUND
	AOJA A,WR2
]POPJ1:	
WR2:	AOS (P)
IFN TS,CBELLS:	POPJ P,[ASCII \\]
IFE TS,	POPJ P,

IFE TS,[RRED:	MOVE A,LFRED1
	MOVE AA,LFRED2
	PUSHJ P,OPNRD
	TYPR1 [ASCII /FILE NOT FOUND
!/]
	POPJ P,


.OPNRD:	PUSHJ P,FRD
.OPNR1:	PUSHJ P,OPNRD"
	TYPR1 [ASCII /FILE NOT FOUND
!/]
	TLO FF,UREAD
	JRST CNTRU1]

IFN TS,[
TSLPTF:	1,,(SIXBIT \LPT\)
	SIXBIT /.TECO./
	SIXBIT /LPTO/
LPTOF:	3,,(SIXBIT /LPT/)
	SIXBIT /.TECO./
	SIXBIT /WPAPER/
FDF:	0
	SIXBIT \.FILE.\
	SIXBIT \(DIR)\
UTOF:	0
	SIXBIT \.TECO.\
	SIXBIT \OUTPUT\
OTTY:	1,,(SIXBIT \TTY\)
	SIXBIT \.TECO.\
	SIXBIT \TYO\
ITTY:	(SIXBIT \TTY\)
	SIXBIT \.TECO.\
	SIXBIT \TYI\
UTF:	3,,(SIXBIT \DSK\)
]UTF1:LFRED1:	SIXBIT /@/
UTF2:LFRED2:	SIXBIT /@/
UTF3:	SIXBIT /@/
UTF4:	SIXBIT /@/

IFN TS,[FFRDEV:TLOA FF,NEGF
]FRD:	IFN TS,TLZ FF,NEGF
	TROA FF,ALTF
FFRRDD:	TRZ FF,ALTF
	MOVE A,UTF1
	MOVE AA,UTF2
	TRO FF,NOTF+FINDR
F1:	MOVEI C,0
	MOVE B,[440600,,C]
F2:	PUSHJ P,LRCH
	CAIE CH,40
	CAIN CH,ALTMOD
	JRST SFN
	CAIE CH,^A
	CAIN CH,^B
	JRST F6
IFN TS,[CAIN CH,";
	JRST SYSN
	CAIN CH,":
	JRST DEVN
]	CAIN CH,^Q
	PUSHJ P,LRCH
	SUBI CH,40
	TLNE B,770000
	IDPB CH,B
	JRST F2

IFN TS,[SYSN:	WSNAME C,
	JRST F1
DEVN:	TRZ FF,FINDR
	HLRM C,UTF
	JRST F1
]
F6:	SKIPA C,UTF1-^A(CH)
SFN:	JUMPL B,SFN1
	TRZN FF,NOTF
	MOVE A,AA
	MOVE AA,C
SFN1:	CAIN CH,40
	JRST F1
FX:	TRZE FF,ALTF
IFN TS,	JRST FX1
IFE TS,	POPJ P,
	MOVEM A,UTF1
	MOVEM AA,UTF2
	POPJ P,
IFN TS,[FX1:	TLZN FF,NEGF
	POPJ P,
	JUMPL B,CPOPJ
	TRNE FF,FINDR
	HLRM AA,UTF
	POPJ P,
]IFN TS,[TAPKIL: HRRZ A,UTF
	CAMN A,ERDEV
	PUSHJ P,UICLS
	CAMN A,EIDEV
	PUSHJ P,UCLOSE
	LDB B,[400,,UTF]
	TLZ FF,CTLUF
	UFLAP B,
	TYPR2 (SIXBIT \UNF\)
	POPJ P,]

IFE TS,[
DELE:	PUSHJ P,FRD
	PUSHJ P,UDELE"
	JRST CNTRU1
]
IFN TS,[
DELE:	PUSHJ P,FFRRDD
DELE1:	SETZM UTF3
DELRNM:	FDELE UTF
	TYPR2 (SIXBIT \FNF\)
	POPJ P,]

IFE TS,[
.FILE:	PUSHJ P,FRD
	PUSHJ P,FILE"
TPFUL:	TYPR1 [ASCII /TAPE FULL!/]
CNTRU1:	TLO FF,CTLUF
	POPJ P,


CLSTP:	TRNN FF,ARG2
	JRST UCLSTP"
	PUSH P,UFPNTR+2
	PUSH P,UFPNTR
	MOVE A,C
	PUSHJ P,.OPN1
	POP P,UFPNTR
	PUSHJ P,UCLSTP
	POP P,A
	PUSHJ P,FILEST
	JRST UTERR
	JRST CNTRU1

MTNAM:	MOVE OUT,[220600+IN,,177]
	MOVE IN,UFPNTR
	HLLZS 177(IN)

MTN1:	PUSHJ P,LRCH
	SUBI CH,40
	JUMPE CH,MTN1
	CAIE CH,175-40
	TLNN OUT,770000
	JRST CNTRU1
	IDPB CH,OUT
	JRST MTN1
]

IFN TS,[
CNTRLV:	PUSHJ P,CNTRU0
	JRST GO1
EUHACK:	PUSHJ P,FFRDEV
CNTRLU:	TRO FF,VIEWF
	PUSHJ P,GDEV
CNTRU0:	MOVEI OUT,DISAD
	PUSHJ P,DISINI
	JRST LSTF2
LISTF:	SKIPE RGETTY
	PUSHJ P,DISINI
	SKIPA OUT,[TYO]
LISTFM:	MOVEI OUT,TYOM
LSTF1:	TRNE CH,20
	PUSHJ P,FFRDEV
LSTF2:	SETOM IN	;FAKE OUT DISBAR TEST
	TLZ FF,CTLUF
	HRRM OUT,LISTF5
	PUSHJ P,AOFDIR
LISTF2:	IOT FDRC,CH
	CAIE CH,EOFCHR
	CAIN CH,14
	JRST LISTF%
	PUSHJ P,@LISTF5
	JRST LISTF2

LISTF%:	TSCLOSE FDRC,
	HRRZ A,LISTF5
	SKIPE RGETTY
	CAIE A,TYO
	CAIN A,DISAD
	JRST DISCLG
	POPJ P,
]

IFE TS,[
LISTFM:	SKIPA OUT,[TYOM]
LISTF:	MOVEI OUT,TYO
	CLEARM LISFLG
	HRRM OUT,LISTF5
CNTRU5:	PUSHJ P,PTLAB
LSTF1A:	SKIPN LISFLG
	JRST LISTF9
	MOVE C,[SIXBIT \TAPE\]
	MOVEI IN,5
	PUSHJ P,TYPR+1
	MOVE B,UFPNTR+2
	PUSHJ P,DPT
	PUSHJ P,LISTF4
LISTF9:	MOVEI B,0
	MOVE A,UFPNTR
	HRLI A,-23.
	SKIPN (A)
	SKIPE 1(A)
	AOJA A,.+2
	AOJA B,.-1
	AOBJN A,.-4
	JUMPN B,.+4
	MOVSI C,(SIXBIT \NO\)
	MOVEI IN,3
	PUSHJ P,TYPR+1
	MOVE OUT,[(600)[SIXBIT /FREE FILES/]-1]
	PUSHJ P,LISTFA
	JUMPE B,.+3
	PUSHJ P,DPT
	PUSHJ P,LISTF4
	MOVE OUT,[(600) [SIXBIT /FREE BLOCKS/]-1]
	PUSHJ P,LISTFA
	HRLZI TT1,-30
	MOVEI A,0
	MOVE D,UFPNTR
	SUBI D,2
	JRST LISTF1



	ADDI D,2
	SKIPN C,(D)
LISTF3:	AOBJN TT1,.-2
	TLNN TT1,-1
	POPJ P,
	HRRZ B,TT1
	SKIPN LISFLG
	JRST .+4
	PUSHJ P,DPT
	MOVEI CH,40
	PUSHJ P,@LISTF5
	ADD TT1,UFPNTR
	MOVE B,55(TT1)
	LDB CH,[(100)104(TT1)]
	DPB B,[(10100)CH]
	SUB TT1,UFPNTR
	ADDI CH,40
	PUSHJ P,@LISTF5
	PUSHJ P,LISTF4
	TLNN C,770000
	PUSHJ P,LISTF4
	TLNE C,770000
	PUSHJ P,TYPR-1
	MOVE C,1(D)
	PUSHJ P,TYPR-1
	HRRZ A,TT1
LISTF1:	SETZB B,E
	MOVE OUT,UFPNTR+1
	ILDB CH,OUT
	CAIN CH,(A)
	AOS E
	CAIE CH,37
	JRST .-4
	SKIPE C,A
	PUSHJ P,LOOK
	SKIPA B,E
	JRST LISTF1+1
	PUSHJ P,DPT
	MOVNS LISFLG
	SKIPGE LISFLG
	PUSHJ P,LISTF4
	SKIPGE LISFLG
	PUSHJ P,LISTF4
	SKIPL LISFLG
	PUSHJ P,CRR1
	JRST LISTF3
]	PUSH P,TYPR2A
TYPR:	MOVEI IN,6
TYPR3:	MOVE OUT,[(600)C-1]
	ILDB CH,OUT
	ADDI CH,40
LISTF5:	PUSHJ P,.
	SOJG IN,.-3
TYPR2A:	POPJ P,LISTF4
LISTFA:	MOVEI IN,12.
	PUSHJ P,TYPR+2
SLTAB:
LISTF4:	MOVEI CH,11
	JRST @LISTF5

TYPA:	MOVE OUT,[(10700)C-1]
TYPA1:	ILDB CH,OUT
	SKIPN CH
	MOVEI CH,40
	PUSHJ P,@LISTF5
	TLNE OUT,760000
	JRST TYPA1
	POPJ P,

LISFLG:	-1
IFE TS,[
RENAM:	PUSH P,C
	PUSHJ P,FRD
	PUSHJ P,.LOOK"
	TYPR1 [ASCIZ /NOT FOUND
!/]
	PUSH P,A+2	;FILE NO
	PUSH P,A+3	;PNTR TO FIRST WD OF NAME
	PUSHJ P,FRD
	PUSHJ P,.LOOK
	JRST RNAM1
	CAME A+3,(P)
	TYPR1 [ASCIZ /ALREADY EXISTS
!/]
RNAM1:	POP P,TT
	MOVEM A,(TT)
	MOVEM A+1,1(TT)
	POP P,A
	POP P,C
	TRNN FF,ARG2
	POPJ P,
	ADD A,UFPNTR"
	DPB C,[(100)104(A)]
	LSH C,-1
	DPB C,[(100)55(A)]
CTYOM:	POPJ P,TYOM

WINIT:	TLO FF,PNSTOP+UWRITE
	PUSHJ P,OPNWR"
	TYPR1 [ASCIZ \NO FREE FILES
!\]
	JRST CNTRU1
]

IFN TS,[
RENAM:	PUSHJ P,FFRRDD
	PUSHJ P,FRD
	MOVEM A,UTF3
	MOVEM AA,UTF4
	JRST DELRNM

SIXNTY:	PUSH P,C
	MOVE C,[440600,,(P)]
SIXNT1:	ILDB CH,C
	JUMPE CH,POPAJ
	ADDI CH,40
	PUSHJ P,@LISTF5
	JRST SIXNT1
]

ETYPER:	0
	SKIPE RGETTY
	PUSHJ P,DIS1
	HLRZS NEWASM
	MOVEM B,ETYPB'
	MOVEM CH,ETYPCH'
	LDB B,[331100,,40]
	CAIN B,TYPR2_-27.
	JRST ETYP2A
	CAIN B,ITYPR_-27.
	JRST ETYP3
	CAIN B,IITYPR_-27.
	JRST ETYPR4
	CAIE B,TYPR1_-27.
	SKIPA B,CBELLS

ETYP1A:	HRRZ B,40
	MOVE P,[-LPDL,,PDL-1]
	HRLI B,440700
	ILDB CH,B
	CAIN CH,"!
	JRST ETYP1
	PUSHJ P,TYO
	JRST .-4

ETYP2:	LDB CH,[140600,,40]
	PUSHJ P,6TYO
	LDB CH,[60600,,40]
	PUSHJ P,6TYO
	LDB CH,[600,,40]
	JRST 6TYO

ETYPR4:	HRRZ B,40
	HRLI B,440600
ETYP4A:	ILDB CH,B
	JUMPE CH,ETYP4B
	PUSHJ P,6TYO
	TLNN B,770000
	JRST ETYP4B
	JRST ETYP4A
IFN TS,[
OPNER1:	MOVEI TT,FHAK
OPNER:	LDB A,[270400+TT,,-2]
	DPB A,[270400,,.+1]
	STATUS .,T
	HRLZ D,@-2(TT)
	IITYPR D
	IITYPR [SIXBIT /:/]
	LDB T,[221000,,T]
	CAIL T,LOMEST-1
	MOVEI T,LOMEST-1
	SKIPL A,OMEST(T)
	JRST OPNER2
	RSNAME D,
	IITYPR D
	IITYPR [SIXBIT /;/]
OPNER2:	TLNE A,200000
	TLZ F,CTLUF
	PUSHJ P,CRR
	TYPR1 @OMEST(T)

]
ETYP1:IFE TS,PUSHJ P,IOWAIT
	HRLZS NEWASM
	JRST IFE TS,[GOZ
]IFN TS,[GOX

OMEST:	[ASCII /ISE0!/]	;4.9=>TYPE SYSTEM NAME
	200000,,[ASCII /NO SUCH DEVICE!/]	;4.8=>DON'T TRY TO ^U
	[ASCII /WRONG DIRECTION!/]
	[ASCII /TOO MANY TRANSLATIONS!/]
	SETZ [ASCII /FILE NOT FOUND!/]
	[ASCII /DIRECTORY FULL!/]
	[ASCII /DEVICE FULL!/]
	200000,,[ASCII /DEVICE NOT READY!/]
	200000,,[ASCII /DEVICE NOT AVAILABLE!/]
	SETZ [ASCII /ILLEGAL FILE NAME!/]
	[ASCII /MODE NOT AVAILABLE!/]
	[ASCII /ISE11!/]

LOMEST==.-OMEST
]

ETYP2A:	MOVE P,[-LPDL,,PDL-1]
	PUSHJ P,ETYP2
	HRLZS NEWASM
	JRST ERRP1

ETYP3:	PUSHJ P,ETYP2
	PUSHJ P,CRR
ETYP4B:	HRLZS NEWASM
	MOVE B,ETYPB
	MOVE CH,ETYPCH
	JRST 2,@ETYPER


IFE TS,[CNTRLV:	PUSHJ P,.OPN2
	PUSH P,CPOPJ
	SETOB IN,LISFLG	;MAKE SURE DISAD TEST FOR DISBAR FAILS
	MOVEI A,40
	HRRM A,DPT5
	PUSH P,CHSIZ
	MOVEI A,20
	MOVEM A,CHSIZ
	MOVEI A,DISAD
	HRRM A,LISTF5
	PUSHJ P,DISINI
	MOVE OUT,UFPNTR"+1
CNTRU4:	MOVEI C,25.
	MOVEI T,0
CNTRU2:	ILDB B,OUT
	CAIN B,37
	JRST CNTRU3
	JUMPE B,CNTRU6
	JUMPE T,CNTR6B
	PUSHJ P,SPSP
	SOJG T,.-1
CNTR6B:	PUSHJ P,SLDPT
CNTR6A:	SOJG C,CNTRU2
	PUSHJ P,CRR1
	JRST CNTRU4

CNTRU6:	ADDI T,3
	JRST CNTR6A

CNTRU3:	PUSHJ P,CNTRU5
CTLUX:	PUSHJ P,DISCLG
	POP P,CHSIZ
	POPJ P,
]
LIGHTS:	TRNE FF,ARG
	JRST LIGT1
IFE TS,	CONI 760,A
IFN TS,	RD760 A,
	JRST VALRET

LIGT1:	
IFE TS,	CONO 760,(B)
IFN TS,	WR760 B,
	JRST RET

IFE TS,BRKFLG:	0
SFINDF:	0
BEG:	0
PT:	0
Z:	0
QRBUF:	0
LEV:	0
COMAX:	0
CPTR:	0
COMCNT:	0
NUM:	0
SYL:	0
OSYL:	0
SARG:	0
LIFF:	0
ERR1:	0
ERR2:	0
BLKOPR:	0
DISPNR:	0
CBUFH:	0
IFE TS,[
TABCNT:	0
BLKOP:	0
BARFL1:	0
TIME:	0
TABCNR:	0
PWOWOT:	0
]
BARPNT:	0
CHCNT:	0
DISBUF:	0
CHCNTS:	0
ITERCT:	0
MEMSIZ:	IFE TS,[IFLE MOBY-170000,[<37000-CBUF+MOBY>*5
]IFG MOBY-170000,[777700
]]IFN TS,<2000*N1KB-CBUF>*5
GCPTR:	0
CRREL:	0
GCFLG:	0
RREL:	0
INTDPH:	0
IFN HLSTSW,[IFN TS,[
SECT:	BLOCK 2*NSECS
SECTPE==.-2
OSECT:	BLOCK 2*NSECS
OSECPE==.-2
]]
IFN TS,[
SYMLPT:	LDB A,[260100,,FF]	;LPTF
	TLZ FF,LPTF
	DPB A,[400100,,LPTSAV]
IFN HLSTSW,[MOVEI CH,OSECT-1
	MOVEM CH,OSECTP
]	AOSA SYMLF
]
SYMLST:	SETZM SYMLF
	TRNE FF,ARG
	JRST BEAST
	SETOM SLN
SYML1:	MOVE CH,Z
	IDIVI CH,5
	ADDI CH,CBUF+10
	IDIVI CH,3	;YES.  WELL, LIE DOWN ON THE COUCH
	IMULI CH,3	;THERE AND WE'LL TALK ABOUT IT.
	MOVEM CH,STPNR
	MOVEM CH,STB
	CLEARM PASS
	MOVEI CH,60
	HRRM CH,DPT5
	PUSHJ P,RRED
NPASS:	MOVEM A,STM	;NO EFFECT ON PASS 1
IFN HLSTSW,SETZM PAGELF
	MOVSI T,1
MNLP2:	MOVE B,[440700,,LBF]
	MOVEM B,LBFIP
	SETOM SLCHS
	CLEARM LSYL
MNLP5:	TLZ FF,DEFF
MNLP:	PUSHJ P,GETSYL
	SKIPGE IFE TS,[BRKFLG] IFN TS,[STOPF]
	JRST SLQ
MNLP3:	TRNN FF,ALTF
	CAIN CH,";
	TROA FF,ALTF
	TRNN FF,LET
	JRST MNLP1
	CAIE CH,"=
	CAIN CH,":
	JRST DEFSM
	TLNN FF,DEFF
	TLNE FF,VARF
	JRST DEFSM
	TLZE FF,GLOF
	JRST DEFGLO
	CAMN SYM,[SIXBIT \DEFINE\]
	TLOA FF,DEFF
	JRST MNLP4

MNLP1:	CAIN CH,14
	JRST SLFF
	CAIE CH,13
	CAIN CH,12
	JRST SLLF
	JRST MNLP
MNLP4:	TRNN FF,ALTF
	SKIPN PASS
	JRST MNLP1

LOGES:	MOVEI D,0
	MOVE A,STB
	MOVE B,STPNR
	SKIPL SYM
	SKIPA B,STM	;LOOK IN LOWER HALF
	MOVE A,STM	;LOOK IN UPPER HALF
LOGES1:	MOVE C,A
	ADD C,B
	IDIVI C,6
	IMULI C,3	;YES, REALLY
	CAMN SYM,(C)
	JRST LOGES2	;FOUND
	CAIL A,-3(B)
	JRST MNLP1	;NOT FOUND
	CAML SYM,(C)
	SKIPA A,C	;LOOK HIGHER
	MOVE B,C	;LOOK LOWER
	JRST LOGES1

LOGES2:	MOVMS D,1(C)
	CAIE CH,")
	MOVEM D,LSYL
	JRST MNLP1

SLRCH:	PUSHJ P,UTYI
	TRZE FF,FINF
	JRST NP1
	CAIE CH,15
	SKIPN PASS
	POPJ P,
	IDPB CH,LBFIP
	AOS SLCHS
	POPJ P,

SLFF:	SKIPN SLCHS
	TRZA FF,ARG\ALTF
	PUSHJ P,SLLF4
IFE HLSTSW,SKIPE PASS
IFE TS,	SKIPE PASS
IFN HLSTSW,[IFN TS,[SKIPE RELAXF
	SKIPN PASS
	SKIPA
]]	PUSHJ P,FORMF
	HLLOS T
IFN HLSTSW,SETZM PAGELF
	AOJA T,MNLP2

SLLF:	PUSHJ P,SLLF4
	TRO FF,ARG
	AOJA T,MNLP2
SYMLF:	0

GETSYL:	MOVE B,[440600,,SYM]
	MOVEI SYM,0
	TDZ FF,[GLOF+VARF,,LET]
GETS6:	PUSHJ P,SLRCH
GETS1:	CAIE CH,"@
	CAIN CH,"!
	JRST GETS6
	CAIN CH,""
	JRST GETSGL
	CAIN CH,"'
	JRST GETSVR
	CAIG CH,"Z
	CAIGE CH,"A
	JRST GETS3
GETS4:	TRO FF,LET
GETS2:	TRC CH,40
	TLNE B,770000
	IDPB CH,B
	JRST GETS6

GETSGL:	TLOA FF,GLOF
GETSVR:	TLO FF,VARF
	TRNE FF,LET
	JRST GETS6
	PUSHJ P,SLRCH
	JRST GETSYL

NP1:	SUB P,[2,,2]
	SKIPE PASS
	JRST SYME
PASS2:IFN HLSTSW,[SETZM RELAXF
	IFN TS,[
	SETOM SECCR
]]
	SETOM PASS
	PUSHJ P,RRED
IFN HLSTSW,IFN TS,SETZM UROSBF
	TRZ FF,ARG
IFN TS,[	SKIPN SYMLF
	JRST LPTW3
LPTW2:	.OPEN UTYOC,LPTOF
	JRST LPTW1
	JSP T,FHAK	;INITIALIZE PRINTER
	JFCL
]
LPTW3:	PUSHJ P,.FNPNT
	CLEARM SLCHS
	MOVE A,STB
	MOVE B,STPNR
	PUSHJ P,SORT
	MOVE A,STB
PASS21:	CAMGE A,STPNR
	SKIPGE (A)
	JRST NPASS
	ADDI A,3
	JRST PASS21

GETS3:	CAIG CH,"9
	CAIGE CH,"0
	CAIN CH,".
	JRST GETS2
	CAIE CH,"%
	CAIN CH,"$
	JRST GETS4
	CAIE CH,";
	POPJ P,
	TRO FF,ALTF
IFN HLSTSW,IFN TS,TRNN FF,COLONF
	POPJ P,
IFN HLSTSW,[IFN TS,[
	HRRZ CH,OSECTP
	SKIPE PASS
	CAIN CH,OSECT-1
	JRST GETS3A
	PUSHJ P,SLRCH
	CAIE CH,";
	JRST GETS1
	AOS CH,SECCR
	ASH CH,1
	SETZM RELAXF
	MOVEM T,SECT(CH)+2
	MOVE CH,OSECT(CH)+2
	TRNN CH,1
	SETOM RELAXF
	JRST GETS6

GETS3A:	MOVEI CH,";
	POPJ P,
]]
IFN TS,[
LPTW1:	SKIPGE STOPF
	JRST GOX
	MOVEI A,30.
	SLEEP A,
	JRST LPTW2
]
SLLF4:	TRZ FF,ALTF
IFN HLSTSW,IFN TS,SKIPG RELAXF
	SKIPN PASS
	POPJ P,
	TRZE FF,ARG
	PUSHJ P,CRR1
	MOVEI B,1(T)
	REPEAT 3,PUSHJ P,SPSP
	PUSHJ P,SLDPT
SLLF1C:	HLLZS SYLCPS
	PUSHJ P,SLTAB
	MOVE D,LSYL
	JUMPE D,SLLF1
	HLRZ B,D
	PUSHJ P,SLDPT
	PUSHJ P,SPSP
	MOVEI B,1(D)
	PUSHJ P,SLDPT

SLLF1:	PUSHJ P,SLTAB
	SOSGE SLCHS
	JRST SLLF2
	MOVE B,[440700,,LBF]
	MOVEM B,LBFIP
SLLF1B:	ILDB CH,LBFIP
	PUSHJ P,PPA
	AOS AA,SYLCPS
	CAIE CH,11
	JRST SLLF1A
	ADDI AA,7
	TRZ AA,7
	HRRM AA,SYLCPS
SLLF1A:	SOSL SLCHS
	JRST SLLF1B
SLLF2:	HRRZ B,T
	IDIVI B,60.
IFN HLSTSW,[TRNE T,-1
	SKIPE PAGELF
]	JUMPN E,CPOPJ
	PUSH P,B
	MOVEI AA,69.
SYLCPS:	SUBI AA,.
	PUSHJ P,SPSP
	SOJG AA,.-1
IFN HLSTSW,	SETOM PAGELF
	MOVE C,[SIXBIT \ PAGE \]
	PUSHJ P,TYPR
	HLRZ B,T
	PUSHJ P,DPT
	POP P,B
	JUMPE B,CPOPJ
	MOVEI CH,".
	PUSHJ P,PPA
	JRST DPT


DEFSM:	SKIPE PASS
	JRST MNLP
	PUSHJ P,ES
	JUMPN D,MNLP5
	MOVNM T,1(F)
	MOVEI D,(SIXBIT \V\)
	TLNE FF,DEFF
	MOVEI D,(SIXBIT \M\)
	TLZE FF,VARF+DEFF
	MOVEM D,2(F)
	TLZN FF,GLOF
	JRST MNLP
DEFGLO:	SKIPE PASS
	JRST MNLP
	PUSHJ P,ES
	MOVEI D,(SIXBIT \G\)
	MOVEM D,2(F)
	JRST MNLP

ES:	PUSH P,T
	PUSH P,TT
	MOVE TT,[AES2,,D]
	BLT TT,TT
	HRR D,STB
	MOVEM SYM,@STPNR
	JRST D

AES2:	CAMN SYM,.	;D
	JRST AES1	;F
	ADDI D,3	;T
	JRST D	;TT

AES1:	POP P,TT
	POP P,T
	HRRZ F,D
	CAME F,STPNR
	JRST AES1A
	CLEARB D,1(F)
	CLEARM 2(F)
	ADDI F,3
	EXCH F,STPNR
	POPJ P,
AES1A:	MOVMS D,1(F)
	POPJ P,

LBFIP:	0
STB:	0
STM:	0
STPNR:	0
PASS:	0
SLN:	0
LSYL:	0
SLCHS:	0
IFN HLSTSW,[RELAXF:	0
SECCR:	-1
PAGELF:	0
]
SYME:	TRZ FF,COLONF
	MOVE A,STB
	MOVE B,STPNR
	MOVEI F,60.
	CAMN A,B
	JRST SLEND
SLPP:	MOVE B,STPNR
	SUBI B,-3(A)
	MOVE AA,[747474747474]
	CAIL B,3*6*60.+3
	JRST SLPP2
	IDIVI B,3*6
	MOVEI AA,1(B)
	IMULI AA,10101
	IMULI B,10101
	HRL AA,AA
	HRL B,B
	ASH E,1
	MOVNI E,-6*6(E)
	ROTC AA,6(E)
SLPP2:	MOVEI F,60.
	MOVEM A,SLCHS
SLPL:	MOVE T,[440600,,AA]
	MOVEI D,6
SLPL1:	SKIPL 2(A)
	CAML A,STPNR
	JRST SLEND
	MOVE C,(A)
	PUSHJ P,TYPR
	PUSHJ P,SPSP
	HRLZ C,2(A)
	MOVEI IN,2
	PUSHJ P,TYPR+1
	HRROS 2(A)
	SKIPN 1(A)
	JRST SLPL2
	SKIPG 1(A)
	TRO FF,SYLF
	MOVMS C,1(A)
	HLRZ B,C
	PUSHJ P,SLDPT
	MOVEI CH,40
	TRZE FF,SYLF
	MOVEI CH,"*
	PUSHJ P,PPA
	MOVEI B,1(C)
	PUSHJ P,SLDPT
SLPL3:	REPEAT 3,PUSHJ P,SPSP
	ILDB B,T
	IMULI B,3
	ADD A,B
	SOJG D,SLPL1
	MOVE A,SLCHS
	ADDI A,3
	MOVEM A,SLCHS
	PUSHJ P,CRR1
	SOJG F,SLPL
	ADDI A,3*5*60.
	JRST SLPP

SLPL2:	MOVE C,[SIXBIT \ UNDEF\]
	PUSHJ P,TYPR
	PUSHJ P,SPSP
	JRST SLPL3

SLEND:IFN HLSTSW,[MOVE A,OSECTP
	SUBI A,OSECT-1
	CAIG A,2
	JRST SLEND1
	PUSHJ P,CRR1
	MOVSI CH,1
	MOVEM CH,SECT
	CAIN F,60.
	JRST SLES1
	MOVEI B,-2(A)
	IDIVI B,12.
	MOVEI E,4(B)
	CAMG E,F
	JRST SLES2
	PUSHJ P,FORMF
SLES1:	MOVEI AA,48.
	PUSHJ P,SPSP
	SOJG AA,.-1
	MOVE C,[SIXBIT \    SE\]
	PUSHJ P,TYPR
	MOVE C,[SIXBIT \CTIONS\]
	PUSHJ P,TYPR
	PUSHJ P,CRR1
	MOVNI F,2
SLES3:	PUSHJ P,CRR1
	MOVEI D,6
SLES4:	ADDI F,2
	MOVE C,OSECT(F)
	PUSHJ P,TYPA
	REPEAT 2,PUSHJ P,SPSP
	HLRZ B,SECT(F)
	PUSHJ P,SLDPT
	PUSHJ P,SPSP
	HRRZ B,SECT(F)
	ADDI B,1
	PUSHJ P,SLDPT
	SOJLE D,SLES3
	MOVEI AA,6
	PUSHJ P,SPSP
	SOJG AA,.-1
	CAILE A,2(F)
	JRST SLES4
]
SLQ:	SKIPN SYMLF
	JRST GOX
	JRST LPTSAV

SLEND1:	PUSHJ P,FORMF
SLEND2:	SKIPGE STOPF
	JRST SLQ
	SOSG SLN
	JRST SLEND4
	MOVEI A,STB
SLEND3:	CAML A,STPNR
	JRST PASS2
	HRRZS 2(A)
	ADDI A,3
	JRST SLEND3
SLEND4:IFN HLSTSW,[MOVEI A,OSECT-1
	MOVEM A,OSECTP
	MOVEI A,SECT-1
	MOVEM A,SECTP
]	SKIPN SYMLF
	JRST RET
IFN TS,[
LPTSAV:	TLO\TLZ FF,LPTF
	PUSHJ P,.FILEX
]
	JRST RET
IFN HLSTSW,[
SLES2:	PUSHJ P,CRR1
	JRST SLES1
]
SORT:	MOVSI C,400000
SORT1:	HRLM B,(P)
	CAIL A,-3(B)
	JRST SORT7
	PUSH P,A
SORT3:	TDNN C,(A)
	JRST SORT4
	SUBI B,3
	TDNE C,(B)
	JRST SORT2
	EXCH C,(A)
	EXCH C,(B)
	EXCH C,(A)
	EXCH C,1(A)
	EXCH C,1(B)
	EXCH C,1(A)
	EXCH C,2(A)
	EXCH C,2(B)
	EXCH C,2(A)
SORT4:	ADDI A,3
SORT2:	CAME A,B
	JRST SORT3
	ROT C,-1
	POP P,A
	JUMPL C,SORT6
	PUSHJ P,SORT1
	HLRZ B,(P)
	PUSHJ P,SORT1
SORT6:	ROT C,1
SORT7:	HLRZ A,(P)
	POPJ P,

.FNPNT:	MOVEI A,PPA
	HRRM A,LISTF5
	PUSHJ P,FORMF
	PUSHJ P,.+1
	MOVE A,LFRED1
	PUSHJ P,.FNPT2
	MOVE A,LFRED2
	PUSHJ P,.FNPT2
FORMF:	MOVEI CH,15
	PUSHJ P,@LISTF5
	MOVEI CH,14
	JRST @LISTF5

.FNPT2:	PUSH P,A
	TRNN FF,ARG
	PUSHJ P,PTLAB
.FN3:	MOVE A,(P)
	MOVEI B,4
	PUSHJ P,CRR1
	SOJN B,.-1
	MOVEI TT1,7
.FN239:	MOVEI D,3
.FN249:	SETZM AA
	ROTC A,6
	MOVEI T,3
.FN259:	XCT LDBT1-1(T)
	IMULI AA,10101
	SETZM C
	TRNE TT,2
	HRLM AA,C
	CAIG T,1
	JRST .FN269
	TRNE TT,1
	HRRM AA,C
.FN269:	PUSHJ P,TYPR
	IDIVI AA,10101
	SOJN T,.FN259
	JUMPE A,.FN279
	MOVEI CH,40
	REPEAT 3,PUSHJ P,PPA
	JRST .FN249
.FN279:	MOVE A,(P)
	PUSHJ P,CRR1
	SOJN D,.FN249
	SOJN TT1,.FN239
POPAJ:	POP P,A
	POPJ P,
PTLAB:	PUSHJ P,CRR1
IFE TS,[MOVE C,UFPNTR
	MOVE C,177(C)
	CAMN C,[-1]
	JRST CRR1
	LSH C,18.
	PUSHJ P,TYPR
	JRST CRR1
]
IFN TS,[PUSHJ P,AOFDIR
PTLAB2:	IOT FDRC,CH
	CAIE CH,15
	CAIN CH,12
	JRST .+3
	PUSHJ P,@LISTF5
	JRST PTLAB2
	TSCLOSE FDRC,
	PUSHJ P,LISTF4
	RSNAME C,
	PUSHJ P,TYPR
	PUSHJ P,LISTF4
	RTIME C,
	DPB C,[301400,,CTIME+1]
	LSH C,-14
	DPB C,[61400,,CTIME]
	LSH C,-14
	DPB C,[301400,,CTIME]
	MOVE C,CTIME
	PUSHJ P,TYPR
	MOVE C,CTIME+1
	PUSHJ P,SIXNTY
	PUSHJ P,LISTF4
	RDATE C,
	DPB C,[221400,CDATE]
	PUSH P,C
	LDB CH,[220100,,(P)]
	IMULI CH,10.
	LDB CH,[140400,,(P)]
	ADD C,CH
	MOVE C,MONTHS-1(C)
	PUSHJ P,SIXNTY
	MOVE C,CDATE
	PUSHJ P,TYPR
	POP P,C
	MOVEI IN,2
	JRST TYPR3

CTIME:	SIXBIT /  :  :  /
CDATE:	SIXBIT / 00,19/
MONTHS:	IRPS S,,[JAN,FEB,MARCH,APRIL
MAY,JUNE,JULY,AUG,SEPT,OCT,NOV,DEC]
	SIXBIT /S/
TERMIN
]

SPSP:	MOVEI CH,40
	JRST @LISTF5

IFN TS,[
AOFDIR:	HRRZ C,UTF
	MOVEM C,FDF
	.OPEN FDRC,FDF
	JSP TT,OPNER
	POPJ P,
]

BEAST:	MOVEM B,SLN
	JUMPG B,SYML1
	MOVEI CH,PPA
	HRRM CH,LISTF5
	PUSHJ P,FRD
	PUSH P,AA
	PUSHJ P,.FNPT2
	POP P,A
	PUSHJ P,.FNPT2
	PUSHJ P,FORMF
	JRST RET

LDBT1:	REPEAT 3,LDB TT,LDBT2-1+.RPCNT*7(TT1)

LDBT2:	REPEAT 21.,[%T1==.RPCNT/7
		%T2==.RPCNT-%T1*7
		CH5.7T(AA+200+<2*%T1+5*%T2>_12.)
		]

CH5.7T:	0	;SP
DEFINE .. A,B,C,D,E,F,G,H
	IFSN H,,[PRINTC /CH5.7T LOSE!
/]
	A_31.+B_26.+C_21.+D_16.+E_11.+F_6+G_1
TERMIN

	.. 4,4,4,4,4,0,4,,	;!
	.. 12,12,12,0,0,0,0,,	;"
	.. 12,12,37,12,37,12,12,,	;#
	.. 4,17,24,16,5,36,4,,	;$
	.. 36,31,2,4,10,23,3,,	;%
	.. 4,12,4,10,25,22,15,,	;&
	.. 4,4,4,0,0,0,0,,	;'
	.. 2,4,10,10,10,4,2,,	;(
	.. 10,4,2,2,2,4,10,,	;)
	.. 0,25,16,33,16,25,0,,	;*
	.. 0,0,4,33,4,0,0,,	;+
	.. 0,0,0,0,14,4,10,,	;,
	.. 0,0,0,16,0,0,0,,	;-
	.. 0,0,0,0,0,14,14,,	;.
	.. 0,1,2,4,10,20,0,,	;/

	.. 16,21,23,25,31,21,16,,	;0
	.. 4,14,4,4,4,4,16,,	;1
	.. 16,21,1,2,4,10,37,,	;2
	.. 16,21,1,6,1,21,16,,	;3
	.. 2,6,12,37,2,2,2,,	;4 . . . OK, BEELER?
	.. 37,20,36,1,1,21,16,,	;5
	.. 16,21,20,36,21,21,16,,	;6
	.. 37,1,2,4,10,20,20,,	;7
	.. 16,21,16,21,21,21,16,,	;8
	.. 16,21,21,17,1,21,16,,	;9
	.. 0,14,14,0,14,14,0,,	;:
	.. 0,14,14,0,14,4,10,,	; ;
	.. 0,2,4,10,4,2,0,,	;<
	.. 0,0,37,0,37,0,0,,	;=
	.. 0,10,4,2,4,10,0,,	;>
	.. 16,21,2,4,4,0,4,,	;?
	.. 0,16,33,25,33,16,0,,	;@
	.. 16,21,21,37,21,21,21,,	;A
	.. 36,21,21,36,21,21,36,,	;B
	.. 16,21,20,20,20,21,16,,	;C
	.. 36,21,21,21,21,21,36,,	;D
	.. 37,20,20,36,20,20,37,,	;E
	.. 37,20,20,36,20,20,20,,	;F
	.. 16,21,20,20,23,21,16,,	;G
	.. 21,21,21,37,21,21,21,,	;H
	.. 16,4,4,4,4,4,16,,	;I
	.. 7,1,1,1,1,21,16,,	;J
	.. 21,22,24,34,22,21,21,,	;K
	.. 20,20,20,20,20,20,37,,	;L
	.. 21,33,25,21,21,21,21,,	;M
	.. 21,21,31,25,23,21,21,,	;N
	.. 16,21,21,21,21,21,16,,	;O
	.. 36,21,21,36,20,20,20,,	;P
	.. 16,21,21,21,25,22,15,,	;Q
	.. 36,21,21,36,21,21,21,,	;R
	.. 16,21,20,16,1,21,16,,	;S
	.. 37,4,4,4,4,4,4,,	;T
	.. 21,21,21,21,21,21,16,,	;U
	.. 21,21,21,21,21,12,4,,	;V
	.. 21,21,21,21,21,25,12,,	;W
	.. 21,21,12,4,12,21,21,,	;X
	.. 21,21,12,4,4,4,4,,	;Y
	.. 37,2,4,16,4,10,37,,	;Z
	.. 6,4,4,4,4,4,6,,	;[
	.. 0,20,10,4,2,1,0,,	;\
	.. 14,4,4,4,4,4,14,,	;]
	.. 4,16,25,4,4,4,4,,	;^
	.. 0,4,10,37,10,4,0,,	;_

	IFN .-CH5.7T-64.,.. ,,,,,,,69


DTB:	
IFE TS,	MOVE A,TIME	;^@
IFN TS,	MOVEI AA,RTIMER	;^@
	MOVEI AA,CXOR	;^A
IFN LPTR,	MOVEI AA,CNTRLB	;^B
IFE LPTR,	TYPR2 (SIXBIT /LPT/)
IFE TS,	MOVEI AA,CLRBF	;^C
IFN TS,	MOVEI AA,TAB	;^C
	HRROI AA,DD	;^D
IFE LPTR,	TYPR2 (SIXBIT /LPT/)
IFE TS+LPTR-1,	TLZ FF,LPTF	;^E
IFN TS,	MOVEI AA,CNTRLE	;^E
IFE TS,	MOVEI AA,LAT	;^F
IFN TS,	RDSW A,		;^F
	MOVEI AA,DECDMP	;^G - TS QUIT
	MOVEI AA,DECDMP	;^H
	MOVEI AA,TAB	;^I - TAB
	MOVEI AA,CD	;^J - LINE FEED
	MOVEI AA,TAB	;^K - VERT TAB
IFE TS,	HRROI AA,TAB	;^L - FORM FEED
IFN TS,	HRROI AA,CTLL	;^L - FORM FEED
	MOVEI AA,CD	;^M - CARR RET
	MOVEI AA,FLDSZ	;^N
	MOVEI AA,SYMLST	;^O
	HRROI AA,TAB	;^P
IFE TS,TLO FF,UREAD	;^Q
IFN TS,MOVEI AA,CONSET	;^Q
	NTS TLZ FF,PNSTOP	;^R
IFE TS,	TLZ FF,UREAD	;^S
IFN TS,	HRROI AA,ASLEEP	;^S
	TLO FF,PNSTOP	;^T
IFE TS,	HRROI AA,.OPN3	;^U
IFN TS,	HRROI AA,CNTRLU	;^U
	TLZ FF,UWRITE	;^V
	TLO FF,UWRITE	;^W
	MOVE A,MARG1	;^X
	MOVE A,MARG2	;^Y
IFE TS,	HRROI AA,ADDT	;^Z
IFN TS,	TYPR2 (SIXBIT \^Z?\)	;^Z
	JRST GO		;^[ - ALT MODE
	MOVEI AA,LIGHTS	;^\
	NTS MOVEI AA,7RI	;^]
	MOVEI AA,CNTRUP	;^^
	NTS MOVEI AA,7WI	;^_

	MOVEI AA,CD2	;
	MOVEI AA,EXCLAM	;!
	MOVEI AA,DQUOTE	;"
	MOVEI AA,COR	;#
	HRROI AA,NEWAS	;$
	MOVEI AA,PCNT	;%
	MOVEI AA,CAND	;&
	MOVEI AA,CD	;'
	MOVEI AA,OPEN	;(
	MOVEI AA,CLOSE	;)
	MOVEI AA,TIMES	;*
	MOVEI AA,CD2	;+
	MOVEI AA,COMMA	;,
	MOVEI AA,MINUS	;-
	JRST PNT	;.
	MOVEI AA,SLASH	;/

	JRST CDNUM	;0
	JRST CDNUM	;1
	JRST CDNUM	;2
	JRST CDNUM	;3
	JRST CDNUM	;4
	JRST CDNUM	;5
	JRST CDNUM	;6
	JRST CDNUM	;7
	JRST CDNUM	;8
	JRST CDNUM	;9
	TRO FF,COLONF	;:
	MOVEI AA,SEMICL	; ;
	MOVEI AA,LSSTH	;<
	HRROI AA,PRNT	;=
	MOVEI AA,GRTH	;>
	MOVEI AA,QUESTN	;?
REPEAT 2,[
IFE TS,	HRROI AA,TAB	;@
IFN TS,	MOVEI AA,SYMLPT	;@
	MOVEI AA,APPEND	;A
	MOVEI A,0	;B
	MOVEI AA,CHARAC	;C
	MOVEI AA,DELETE	;D
	HRROI AA,ECMD	;E
	NTS HRROI AA,FEED	;F
	MOVEI AA,QGET	;G
	MOVEI AA,HOLE	;H
	HRROI AA,INSERT	;I
	MOVEI AA,JMP	;J
	MOVEI AA,KILL	;K
	MOVEI AA,LINE	;L
	JRST MAC	;M
	MOVEI AA,SERCHP	;N
	MOVEI AA,OG	;O
	HRROI AA,PUNCH	;P
	MOVEI AA,QREG	;Q
	MOVEI AA,REVERS	;R
	MOVEI AA,SERCH	;S
	HRROI AA,TYPE	;T
	MOVEI AA,USE	;U
	MOVEI AA,VIEW	;V
	MOVEI AA,CD	;W
	MOVEI AA,X	;X
	HRROI AA,YANK	;Y
	MOVEI AA,END1	;Z
	MOVEI AA,OPENB	;[
	MOVEI AA,BAKSL	;\
	MOVEI AA,CLOSEB	;]
	TRO FF,SLSL	;^
IFE .RPCNT,	MOVEI AA,LARR	;_
]IFN .-DTB-177,[PRINTX \DTB LOSS
\]	MOVEI AA,TAB

IFN TS,[
0
UTIBUF:	BLOCK 100
UTIBE:	14_22.

UTOBUF:	BLOCK 100
UTOBE:
UTYIP:	0
UTYOP:	0
UTYOCT:	0
]
ADSFL:	0
STAB:
LBF:
AC2:	BLOCK 16
BAKTAB:	BLOCK 3
	LOC STAB+100

STABP:
SYMS:	BLOCK 22
VALS:	BLOCK 22
CNTS:	BLOCK 22
SYMEND:
PFL:	BLOCK LPF
GCTAB:	BLOCK GCTBL
QTAB:	BLOCK NQREG
PDL:	BLOCK LPDL+4	;FOR RANDOMNESS
IFE TS,[
PUNBO:	BLOCK 100
PUNBE:	0
RBUF:	BLOCK 250
RBEND:	0
RDCHAR=<RBEND-RBUF>*5
]
CONSTANTS
VARIABLES
CBUF:
IFN TS,N1KB==CBUF/2000+IFN .&1777,1
END INIT


